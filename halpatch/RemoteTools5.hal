external function Boolean EarlierReservationsExists(record ORVc);
external function string 20 FindJobCustomerCode(record JobVc);
external function Boolean GetCostAcc(string,string,record AccBlock,record INVc,integer,var string,var string);
external procedure SHMakeStockMovements(var Array record StockMovVc,var Integer,record SHVc,row SHVc,record INVc,string,string,string,string,Integer,record MainWHMBlock);
external function string 20 NextTransportSerNr(string,LongInt,string);
external function Boolean PasteBatchSerItems_DemandPosition(var Array record StockMovVc,Integer,var record SHVc,var Integer);
external updating function Boolean StoreShipmentStockMovements(var record SHVc,Array record StockMovVc,var Integer,Boolean);
external procedure CheckFlush(var Integer,Integer);
external function LongInt FindReservation(string,Date,Time);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external procedure GetNextProjectNr(var string);
external procedure SetORFlags(record ORVc);
external updating function LongInt CreateCOInv(record RcVc,Boolean,var Boolean);
external function LongInt GetCurUserLastNr(string);
external procedure SHSumUp(var record SHVc);
external function Boolean PasteBatchSerItems(var string,var Date,var Integer,var val,record ORVc,Integer,var record SHVc,Integer,string,Integer,var Integer);
external procedure FindBatchBestBeforeDate(string,string,var Date);
external function Boolean GetCustAndBal(var record CUVc,var val,var val,Integer,Integer,Integer,Integer,Integer,Integer);
//external procedure FindStockValue(string,string,var record ItemStatusVc);
external procedure FindStockValueInclStockReserv(LongInt,string,string,string,var record ItemStatusVc,var val);
external function Boolean GetItemStatus(string,string,string,var val,var val,var val,var val,var val,var val);
external procedure GetUserName(string,var string);
external procedure NextM4Number(string,var string);

global
function Boolean PasteCustInShip(record SHVc SHp,string clcd)
BEGIN
  record CUVc CUr;
  Boolean found;
  
  CUr.Code = clcd;
  found = ReadFirstMain(CUr,1,true);
  if (found) then begin
    SHp.Addr0 = CUr.Name;
    if (blank(CUr.DelAddr0) and blank(CUr.DelAddr1) and blank(CUr.DelAddr1)) then begin
      SHp.Addr1 = CUr.InvAddr0;
      SHp.Addr2 = CUr.InvAddr1;
      SHp.Addr3 = CUr.InvAddr2;
      SHp.DelAddr3 = CUr.InvAddr3;
      SHp.DelAddr4 = CUr.InvAddr4;
    end else begin
      SHp.Addr1 = CUr.DelAddr0;
      SHp.Addr2 = CUr.DelAddr1;
      SHp.Addr3 = CUr.DelAddr2;
      SHp.DelAddr3 = CUr.DelAddr3;
      SHp.DelAddr4 = CUr.DelAddr4;
    end;
  end;
  PasteCustInShip = found;
  RETURN;
END;

global
function Boolean PositionInDeliveryArea(string poscode,string DeliveryPosArea)
BEGIN
  Boolean res;
  record PosVc Posr;
  
  Posr.Code = poscode;
  if (ReadFirstMain(Posr,1,true)) then begin
    if (nonblank(Posr.LocArea)) then begin
      if (Posr.LocArea==DeliveryPosArea) then begin
        res = true;
      end;
    end;
  end;
  PositionInDeliveryArea = res;
  RETURN;
END;

global
updating function Boolean SetNewPositionOnStockMov(LongInt StockMovNr,Boolean frf,string poscode)
BEGIN
  Boolean res;
  record StockMovVc StockMovr;
  row StockMovVc StockMovrw;
  record StockMovVc oldStockMovr;
  Integer i,rwcnt;
  
  StockMovr.SerNr = StockMovNr;
  if (ReadFirstMain(StockMovr,1,true)) then begin
    if (IsRecordLocked(StockMovr)) then begin goto LSetNewPositionOnStockMov; end;
    if (StockMovr.OKFlag!=0) then begin goto LSetNewPositionOnStockMov; end;
    RecordCopy(oldStockMovr,StockMovr);
    rwcnt = MatRowCnt(StockMovr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(StockMovr,i,StockMovrw);
      if (frf) then begin
        StockMovrw.FrPosCode = poscode;
      end else begin
        StockMovrw.ToPosCode = poscode;
      end;
      MatRowPut(StockMovr,i,StockMovrw);
    end;
    if (RecordUpdate(oldStockMovr,StockMovr,true)==0) then begin 
      res = true;
    end else begin
    end;
  end;
LSetNewPositionOnStockMov:;  
  SetNewPositionOnStockMov = res;
  RETURN;
END;

updating 
procedure SetNewQuantityOnSH(record StockMovVc StockMovr,Integer stmovrow,val newqty,val oldqty,var Integer shrownr)
BEGIN
  row StockMovVc StockMovrw;
  record SHVc SHr;
  record SHVc oldSHr;
  row SHVc SHrw;
  Integer i,rwcnt;

  shrownr = -1;  
  if (StockMovr.FileName=="SHVc") then begin
    if (StockMovr.TransNr>0) then begin
      MatRowGet(StockMovr,stmovrow,StockMovrw);
      SHr.SerNr = StockMovr.TransNr;
      if (ReadFirstMain(SHr,1,true)) then begin
        if (SHr.OKFlag==0) then begin
          RecordCopy(oldSHr,SHr);
          rwcnt = MatRowCnt(SHr);
          for (i=0;i<rwcnt;i=i+1) begin
//            if (i==StockMovrw.SHRow) then begin //SHRow is very wrong
              MatRowGet(SHr,i,SHrw);
              if (SHrw.ArtCode==StockMovrw.ArtCode) and (SHrw.Ship==oldqty) then begin
                if (newqty<SHrw.Ship) then begin
//                  SHrw.Ordered = newqty;
                end;
                SHrw.Ship = newqty;
                MatRowPut(SHr,i,SHrw);
                shrownr = i;
                i = rwcnt;
              end;
//            end;
          end;
          if (RecordUpdate(oldSHr,SHr,true)==0) then begin
          end;
        end;
      end;
    end;
  end;
  RETURN;
END;

global
updating function Boolean SetNewQuantityOnStockMov(LongInt StockMovNr,string artcode,val newqty,var Integer shrownr)
BEGIN
  Boolean res;
  record StockMovVc StockMovr;
  row StockMovVc StockMovrw;
  record StockMovVc oldStockMovr;
  Integer i,rwcnt,stmovrow;
  val oldqty;
  
  StockMovr.SerNr = StockMovNr;
  if (ReadFirstMain(StockMovr,1,true)) then begin
    if (IsRecordLocked(StockMovr)) then begin goto LSetNewQuantityOnStockMov; end;
    if (StockMovr.OKFlag!=0) then begin goto LSetNewQuantityOnStockMov; end;
    RecordCopy(oldStockMovr,StockMovr);
    rwcnt = MatRowCnt(StockMovr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(StockMovr,i,StockMovrw);
      if (StockMovrw.ArtCode==artcode) then begin
        oldqty = StockMovrw.Quant;
        StockMovrw.Quant = newqty;
        MatRowPut(StockMovr,i,StockMovrw);
        stmovrow = i;
        i = rwcnt;
      end;
    end;
    if (RecordUpdate(oldStockMovr,StockMovr,true)==0) then begin 
      res = true;
      SetNewQuantityOnSH(StockMovr,stmovrow,newqty,oldqty,shrownr);
    end else begin
    end;
  end;
LSetNewQuantityOnStockMov:;  
  SetNewQuantityOnStockMov = res;
  RETURN;
END;

procedure FillPositionsOnSH(record SHVc SHp,var Integer rownr,Array record StockMovVc aStockMovr,Integer acnt,record INVc INp,string DeliveryPosCode)
BEGIN
  Integer ai;
  Integer i,rwcnt;
  record StockMovVc StockMovr;
  row StockMovVc StockMovrw;
  row SHVc SHrw;
  row SHVc oldSHrw;
  row SHVc newSHrw;
  val rem,instock;
  Integer shrow;
  
  shrow = rownr;
  MatRowGet(SHp,rownr,oldSHrw);
  instock = oldSHrw.InStock;
  for (ai=0;ai<acnt;ai=ai+1) begin
    StockMovr = aStockMovr[ai];
    rwcnt = MatRowCnt(StockMovr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(StockMovr,i,StockMovrw);
      if (StockMovrw.SHRow==shrow) and (StockMovrw.ArtCode==oldSHrw.ArtCode) then begin
        CopyRow(SHp,oldSHrw,newSHrw);
        newSHrw.InStock = instock;
        newSHrw.Ship = StockMovrw.Quant;
        newSHrw.Ordered = StockMovrw.Quant;
        newSHrw.PosCode = StockMovrw.ToPosCode;
        if (rownr==shrow) then begin
          MatRowPut(SHp,rownr,newSHrw);
        end else begin
          MatRowPut(SHp,rownr,newSHrw);
        end;
        rownr = rownr + 1;
        instock = instock - newSHrw.Ship;
        
        StockMovrw.SHRow = rownr - 1;
        MatRowPut(StockMovr,i,StockMovrw);
      end;
    end;
    aStockMovr[ai] = StockMovr;
  end;
  if (rownr>0) then begin
    rownr = rownr - 1;
  end;
  RETURN;
END;

procedure FillupShipRowsWithPositions(var Array record StockMovVc aStockMovr,var Integer acnt,var record SHVc SHp,record INVc INp,var Integer rownr,string MainStock,string DeliveryPosCode,string DeliveryArea,string warehouse,record MainWHMBlock MainWHMRec)
BEGIN
  row SHVc SHrw;
  row SHVc pSHrw;
  record ItemStatusVc ISr;
  record PISVc PISr;
  record PISVc pPISr;
  Boolean TrHs;
  string 20 location;
  val rem,atpos;
  Integer rw;
  record INVc locINr;
  val shqty;
  
  rw = rownr;
  MatRowGet(SHp,rownr,SHrw);
  if (nonblank(DeliveryPosCode)) then begin
    SHMakeStockMovements(aStockMovr,acnt,SHp,SHrw,INp,MainStock,DeliveryPosCode,DeliveryArea,warehouse,rownr,MainWHMRec);
    FillPositionsOnSH(SHp,rownr,aStockMovr,acnt,INp,DeliveryPosCode);
  end;
  RETURN;
END;

function val ReduceFromReservationArray(string incode,string serialnr,string location,var array string aincode,var array string aloc,var array string aserialnr,var array val aqty,var Integer acnt)
begin
  val res;
  Integer i;
  
  res = 0;
  for (i=0;i<acnt;i=i+1) begin
    if (aincode[i]==incode and aloc[i]==location and aserialnr[i]==serialnr) then begin
      res = res + aqty[i];
      i = acnt;
    end;
  end;
  ReduceFromReservationArray = res;
  return;
end;

global
Function Boolean RefillShipRow(var Array record StockMovVc aStockMovr,var Integer asmcnt,var record SHVc SHp,record SHVc SH2p,record INVc INp,var Integer rownr,Integer rwcnt,Integer wa,var array string aincode,var array string aloc,var array string aserialnr,var array val aqty,var Integer acnt)
BEGIN
  Boolean res;
  record MainStockBlock MainStockRec;
  record MainWHMBlock MainWHMRec;
  record ORVc ORr;
  record ItemStatusVc ISr;
  row SHVc SHrw,SH2rw,SHoldrw;
  string 255 tstr,location,oldlocation;
  Integer j;
  val t,t2;
  Integer reserved;
  Boolean testf;
  val instock,stockreservq;
  record CostAccBlock CostAccRec;
  record AccBlock ARAccRec;
  string 20 costacc;

  res = true;
  BlockLoad(MainStockRec);
  BlockLoad(MainWHMRec);
  BlockLoad(CostAccRec);
  BlockLoad(ARAccRec);
  ORr.SerNr = SHp.OrderNr;
  if (ReadFirstMain(ORr,1,true)) then begin
    reserved = ORr.Reserved;
  end;
  MatRowGet(SHp,rownr,SHrw);
  INp.Code = SHrw.ArtCode;
  if (ReadFirstItem(SHrw.ArtCode,INp,true,true)) then begin
    location = SHp.Location;
    if (nonblank(SHrw.Location)) then begin
      location = SHrw.Location;
    end;
    if (INp.ItemType==1) then begin
      SHrw.Coefficient = INp.UnitCoefficient;
      if (GetItemStatus(SHrw.ArtCode,location,"",t,t2,t2,t2,t2,t2)) then begin end;
      FindStockValueInclStockReserv(ORr.SerNr,SHrw.ArtCode,SHrw.SerialNr,location,ISr,stockreservq);
      instock = ISr.Instock;
      if (wa!=0) then begin
        switch (MainStockRec.UpdateAvailable) begin
          case 1:
            if (SH2p.CustCode!="NIL") then begin
              MatRowGet(SH2p,rownr,SHoldrw);
              oldlocation = SH2p.Location;
              if (nonblank(SHoldrw.Location)) then begin
                oldlocation = SHoldrw.Location;
              end;
              if (blank(oldlocation)) then begin
                oldlocation = MainStockRec.MainStock;
              end;
              if (location==oldlocation) then begin 
                if (instock!=0) then begin t = t + SHoldrw.Ship; end;
              end;
            end;  
        end;  
      end;  
      if (reserved==0) then begin
        t = t - ISr.RsrvQty;
      end else begin
//        t = t - SHrw.Ordered;//what ? if smthing is rsrved on curent order why to deduct it ? 
      end;
      if (stockreservq!=0) then begin
        t = t - stockreservq;
      end;
      t = t - ReduceFromReservationArray(SHrw.ArtCode,SHrw.SerialNr,location,aincode,aloc,aserialnr,aqty,acnt);
      SHrw.InStock = t;
      for (j=0;j<rownr;j=j+1) begin
        MatRowGet(SHp,j,SH2rw);
        testf = false;
        if ((SHrw.ArtCode==SH2rw.ArtCode) and (SHrw.OrdRow!=SH2rw.OrdRow)) then begin
          testf = true;
        end;
        if (testf) then begin
          testf = false;
          if (blank(SHrw.Location)) then begin
            if (blank(SH2rw.Location)) then begin testf = true; end;
            if ((nonblank(SH2rw.Location)) and (SHp.Location==SH2rw.Location)) then begin testf = true; end;
          end else begin
            if (SHrw.Location==SH2rw.Location) then begin testf = true; end;
            if ((blank(SH2rw.Location)) and (SHp.Location==SHrw.Location)) then begin testf = true; end;
          end;
          if (testf) then begin SHrw.InStock = SHrw.InStock - SH2rw.Ship; end;
        end;
      end;
      if (SHrw.InStock<SHrw.Ship) then begin
        SHrw.Ship = SHrw.InStock;
      end;
      if (MainStockRec.dontAllowOvership==2) then begin
        SHrw.Ship = SHrw.Ordered;
      end;
      if (SHrw.Ship<0) then begin
        SHrw.Ship = 0;
      end;
    end else begin
      if (nonblank(SHrw.Recepy)) then begin
        tstr = "_";
        if (SHrw.Recepy!=tstr) then begin
          if (GetItemStatus(SHrw.ArtCode,location,SHrw.Recepy,t,t2,t2,t2,t2,t2)) then begin end;
          SHrw.InStock = t;
          if (SHrw.InStock<SHrw.Ship) then begin
            SHrw.Ship = SHrw.InStock;
          end;
          if (MainStockRec.dontAllowOvership==2) then begin
            SHrw.Ship = SHrw.Ordered;
          end;
          if (SHrw.Ship<0) then begin
            SHrw.Ship = 0;
          end;
        end;
      end;
      if (INp.ItemType==0) then begin
        SHrw.Ship = SHrw.Ordered;
      end;
    end;
    if (GetCostAcc(SHrw.CostAcc,SHp.CustCode,ARAccRec,INp,CostAccRec.ItemGroupAccounts,costacc,tstr)==true) then begin
    end;
    SHrw.CostAcc = costacc;
    MatRowPut(SHp,rownr,SHrw);
  end else begin
    if (nonblank(SHrw.ArtCode)) then begin
      res = false;
    end;
    INp.Weight = blankval;
    INp.Volume = blankval;
    INp.SerNrf = 0;
    SHrw.Ordered = blankval;
    SHrw.InStock = blankval;
    SHrw.Ship = blankval;
    MatRowPut(SHp,rownr,SHrw);
  end;
  if (res) then begin
    testf = false;
    if (SHp.Location==MainWHMRec.Location) then begin testf = true; end;
    if (blank(SHp.Location)) then begin
      if (MainStockRec.MainStock==MainWHMRec.Location) then begin testf = true; end;
    end;
    if (INp.ItemType!=0) then begin
      if ((MainWHMRec.DemandPosition!=0) and (wa==0) and (testf)) then begin
        FillupShipRowsWithPositions(aStockMovr,asmcnt,SHp,INp,rownr,MainStockRec.MainStock,MainWHMRec.DeliveryPosCode,MainWHMRec.DeliveryArea,MainWHMRec.Location,MainWHMRec);
      end;
    end;
  end;
  RefillShipRow = res;
  RETURN;
END;

/* This is VERY confusing... This means that several Deliveries are created, but only one pops up on screen (if you use Special Menu) 
If we don't have it we create delivery with 600 rows for example, should we do it?
Lucy wanted me to fix it
*/
updating function Boolean SaveShipment(Array record StockMovVc aStockMovr,var Integer asmcnt,record SHVc SHp,record CUVc CUr,record ORVc ORr,record PlanDeliveryBlock PlanDelRec,var val totv,var val totw,var val totq,var Integer srw)
BEGIN
  Boolean res;
  LongInt ornr,newnr;
  record SRBlock SRRec;
  Date dat;
  Integer i;
  
  res = true;
  ornr = SHp.OrderNr;
  if ((MatRowCnt(SHp)>0) and (SHp.SerNr!=-1)) then begin
    SHSumUp(SHp);
    if (RecordInsert(SHp,false)) then begin
      res = StoreShipmentStockMovements(SHp,aStockMovr,asmcnt,false);
      if (res==false) then begin goto LSaveShipment; end;
      srw = 0;
      // Edit Start ---------------------------------------------- Edit Start
	//Tuesday, 26 April 2011 13:21:58
	CreateRecordLink(ORr,CurrentCompany,SHp,CurrentCompany);  
	CreateRecordLink(SHp,CurrentCompany,ORr,CurrentCompany);  
	// Edit End ---------------------------------------------- Edit End
	
      RecordNew(SHp);
      SHp.SerNr = -1;
      SHp.OrderNr = ornr;
      if (SHp.SerNr==-1) then begin
        BlockLoad(SRRec);
        newnr = GetCurUserLastNr("SHVc");
        if (newnr!=-1) then begin
          newnr = SRRec.LastShipNr;
        end;
        SHp.SerNr = NextSerNr("SHVc",SHp.ShipDate,newnr,false,"");
        if (SHp.SerNr==-1) then begin
          res = false;
        end;
      end;
      CUr.Code = ORr.CustCode;
        switch (CUr.DeliveryBasedOn) begin
         case 1:
           if (nonblank(ORr.PlanShip)) then begin
             SHp.ShipDate = StringToDate(ORr.PlanShip);
           end;
        end;
        if (CUr.OnHoldFlag!=0) then begin 
          if (ISMAINTENANCE==false) then begin
            res = 1300;
          end;
          goto LSaveShipment; 
        end;
        if (CUr.blockedFlag!=0) then begin
          if (ISMAINTENANCE==false) then begin
            res = 1265;
          end;
          goto LSaveShipment;
        end;
     if (PlanDelRec.PreventDelivery!=0) then begin
       dat = StringToDate(ORr.PlanShip);
       if (SHp.ShipDate<dat) then begin
         goto LSaveShipment;
        end;
     end;
     SHp.OrderNr = ORr.SerNr;
     SHp.CustCode = ORr.CustCode;
     if (PasteCustInShip(SHp,SHp.CustCode)) then begin end;
     if (nonblank(ORr.Addr0)) then begin
       SHp.Addr0 = ORr.Addr0;
     end;
     if ((nonblank(ORr.ShipAddr0)) or
        (nonblank(ORr.ShipAddr1)) or
        (nonblank(ORr.ShipAddr2)) or
        (nonblank(ORr.ShipAddr3))) then begin
        SHp.Addr0 = ORr.ShipAddr0;
        SHp.Addr1 = ORr.ShipAddr1;
        SHp.Addr2 = ORr.ShipAddr2;
        SHp.Addr3 = ORr.ShipAddr3;
        SHp.DelAddr3 = ORr.DelAddr3;
        SHp.DelAddr4 = ORr.DelAddr4;
      end;
      SHp.Packages = blankval;
      SHp.ShipMode = ORr.ShipMode;
      SHp.TransportNumber = NextTransportSerNr("SHVc",SHp.SerNr,SHp.ShipMode);
      SHp.LangCode = ORr.LangCode;
      SHp.PRCode = ORr.PRCode;
      SHp.Location = ORr.Location;
      SHp.Objects = ORr.Objects;
      SHp.Sorting = ORr.Sorting;
      SHp.DelAddrCode = ORr.DelAddrCode;
      SHp.Region = ORr.Region;
    end;
  end;
  totv = 0;
  totw = 0;
  totq = 0;
LSaveShipment:;  
  SaveShipment = res;
  RETURN;
END;

procedure SetBackOrderComment(var record SHVc SHr)
begin
  record SHVc SH2r;
  boolean TrHs, testf;
  record OrdSettBlock OSBr;
  
  BlockLoad(OSBr);
  if (nonblank(OSBr.BackOrderComment)) then begin
    SH2r.OrderNr = SHr.OrderNr;
    TrHs = true;
    while (LoopKey("OrderKey",SH2r,1,TrHs)) begin
      if (TrHs) then begin
        if (SH2r.OrderNr <> SHr.OrderNr) then begin
          TrHs = false;
        end;
      end;
      if (TrHs) then begin
        testf = true;
        if (SH2r.SerNr == SHr.SerNr) then begin
          testf = false;
        end;
        if (SH2r.TotQty == 0) then begin
          testf = false;
        end;
        if (testf) then begin
          SHr.Comment = OSBr.BackOrderComment;
          goto LFindOldShipment;
        end;
      end;
    end;
  end;
  LFindOldShipment:;
  return;
end;

global
updating function Integer PasteOrdInShip(var Array record StockMovVc aStockMovr,var Integer asmcnt,record ORVc ORr,var record SHVc SHp,Boolean checklimitf,string planneddelivery,Integer skipzerorows,string onlyforitems,array string aincode,var array string aloc,var array string aserialnr,array val aqty,Integer acnt)
BEGIN
  record CreditLimitBlock CreditLimitRec;
  record PlanDeliveryBlock PlanDelRec;
  record MainStockBlock mstr;
  record INVc INr;
  record CUVc CUr;
  record SHVc NILSHr;
  row ORVc ORrw;
  row SHVc SHrw;
  Integer i,rwcnt;
  Integer srw,shrwcnt;
  val remn,totq,totw,totv,t;
  val bal,limit;
  val ordered,stock,t1;
  string 60 cursernr;
  Boolean dontdeletef,testf;
  Date dat,planneddeliverydat;
  Integer res;
  record MainWHMBlock MainWHMRec;
  string 50 frin,toin;
  Date curbestbef,bd;
  record ObjectsAccBlock OAb;//Edit***************************Sasha2,13:08 27.06.2014
  row ObjectsAccBlock OArw;//Edit***************************Sasha2,13:08 27.06.2014
  Integer j, mtrw; //Edit***************************Sasha2,13:14 27.06.2014
  
  frin = FirstInRange(onlyforitems,20);
  toin = LastInRange(onlyforitems,20);
  res = -1;  
  BlockLoad(mstr);
  BlockLoad(PlanDelRec);
  BlockLoad(CreditLimitRec);
  BlockLoad(MainWHMRec);
  if (ORr.Location==MainWHMRec.Location) and (nonblank(MainWHMRec.Location)) then begin 
    if (ISMAINTENANCE==false) then begin
      res = 2299;
      goto LPasteOrdInShip;
    end;
  end;
  
  if (checklimitf) then begin
    if (CreditLimitRec.SHSave==1) then begin
      CUr.Code = ORr.CustCode;
      if (GetCustAndBal(CUr,limit,bal,CreditLimitRec.Base,CreditLimitRec.OwnCheques,CreditLimitRec.ThirdCheques,CreditLimitRec.IOUCheques,CreditLimitRec.ThirdIOUCheques,CreditLimitRec.ORSaveWarn)) then begin
      end;
//      bal = bal + ORr.Sum4;//done in GetCustAndBal
      if (limit>0) then begin
        if (bal>limit) then begin
          res = 1164;
          goto LPasteOrdInShip;
        end;
      end;
    end;
  end;
  t1 = 1;
  CUr.Code = ORr.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin
    switch (CUr.DeliveryBasedOn) begin
     case 1:
       if (nonblank(ORr.PlanShip)) then begin
         SHp.ShipDate = StringToDate(ORr.PlanShip);
       end;
    end;
    if (CUr.OnHoldFlag!=0) then begin 
      if (ISMAINTENANCE==false) then begin
        res = 1300;
      end;
      goto LPasteOrdInShip; 
    end;
    if (CUr.blockedFlag!=0) then begin
      if (ISMAINTENANCE==false) then begin
        res = 1265;
      end;
      goto LPasteOrdInShip;
    end;
  end;    
  if (PlanDelRec.PreventDelivery!=0) then begin
    dat = StringToDate(ORr.PlanShip);
    if (SHp.ShipDate<dat) then begin
      goto LPasteOrdInShip;
    end;
  end;
  SHp.OrderNr = ORr.SerNr;
  SHp.CustCode = ORr.CustCode;
  if (PasteCustInShip(SHp,SHp.CustCode)) then begin end;
  if (nonblank(ORr.Addr0)) then begin
    SHp.Addr0 = ORr.Addr0;
  end;
  if ((nonblank(ORr.ShipAddr0)) or
        (nonblank(ORr.ShipAddr1)) or
        (nonblank(ORr.ShipAddr2)) or
        (nonblank(ORr.ShipAddr3))) then begin
      SHp.Addr0 = ORr.ShipAddr0;
      SHp.Addr1 = ORr.ShipAddr1;
      SHp.Addr2 = ORr.ShipAddr2;
      SHp.Addr3 = ORr.ShipAddr3;
      SHp.DelAddr3 = ORr.DelAddr3;
      SHp.DelAddr4 = ORr.DelAddr4;
  end;
  SHp.Packages = blankval;
  totq = blankval;
  totv = blankval;
  totw = blankval;
  SHp.ShipMode = ORr.ShipMode;
  SHp.TransportNumber = NextTransportSerNr("SHVc",SHp.SerNr,SHp.ShipMode);
  SHp.LangCode = ORr.LangCode;
  SHp.PRCode = ORr.PRCode;
  SHp.Location = ORr.Location;
  SHp.Objects = ORr.Objects;
  SHp.Sorting = ORr.Sorting;
  SHp.DelAddrCode = ORr.DelAddrCode;
  SHp.Region = ORr.Region;
  SHp.Comment = ORr.Comment;
  rwcnt = MatRowCnt(ORr);
  srw = 0;
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(ORr,i,ORrw);
    remn = ORrw.Quant - ORrw.Shipd1;
    if (ORrw.stp!=1) then begin
      goto LSKIPROW;
    end;
    ordered = remn;
    testf = true;
    if (PlanDelRec.PreventDelivery!=0) then begin
      if (nonblank(ORrw.PlanShipRow)) then begin
        dat = StringToDate(ORrw.PlanShipRow);        
        if (SHp.ShipDate<dat) then begin
          testf = false;
        end;
      end;
    end;
    if (nonblank(planneddelivery)) then begin
      planneddeliverydat = StringToDate(planneddelivery);
      if (nonblank(ORrw.PlanShipRow)) then begin
        dat = StringToDate(ORrw.PlanShipRow);        
      end else begin
        if (nonblank(ORr.PlanShip)) then begin
          dat = StringToDate(ORr.PlanShip);        
        end;
      end;
      if (nonblankdate(dat)) then begin
        if (dat>planneddeliverydat) then begin
          testf = false;
        end;
      end;
    end;
    if (testf) then begin
      if (nonblank(onlyforitems)) then begin
        if (Left(ORrw.ArtCode,len(frin))<frin) then begin testf = false; end;
        if (Left(ORrw.ArtCode,len(toin))>toin) then begin testf = false; end;
      end;
    end;
    if (( ((remn>0) and (remn!=blankval)) or
        ((remn>0) and (ORrw.Sum==0)) or      // otherwise overdelivered orders are really making strange SH
        ((remn<0) and (ORrw.Quant==remn)) or // to allow delivering of negative plain items on order, which is only allowed if Negative Quantities on Invoices is turned on.
        (blank(ORrw.ArtCode))) and (testf)) then begin
      stock = blankval;
      cursernr = "";
      curbestbef = bd;
L10PasteOrdInShip:;
      ClearRow(SHp,SHrw,1);
      SHrw.OrdRow = i;
      SHrw.BBCostPrice = ORrw.BasePrice;
      SHrw.ArtCode = ORrw.ArtCode;
      SHrw.Spec = ORrw.Spec;
      SHrw.Objects = ORrw.Objects;
      blockload(OAb); //Edit***************************Sasha2,13:13 27.06.2014
	  mtrw = matrowcnt(OAb);
	  For(j=0;j<mtrw;j=j+1)begin
	    matrowget(OAb,j,OArw);
	    if(setinset(OArw.Objects,ORr.Objects))then begin
	      SHrw.CostAcc = OArw.AccCode;
	      j = mtrw;                      
	    end;
	  end; //Edit***************************Sasha2,13:13 27.06.2014
      SHrw.SerialNr = ORrw.SerialNr;
      SHrw.Recepy = ORrw.Recepy;
      SHrw.VARList = ORrw.VARList;
      SHrw.Location = ORrw.Location;
      SHrw.Source = ORrw.Source;
      SHrw.DespatchRowTime = ORrw.DespatchRowTime;
      if (nonblankdate(ORrw.DespatchRowDate)) then begin
        SHrw.DespatchRowDate = ORrw.DespatchRowDate;
      end else begin
        SHrw.DespatchRowDate = ORr.DespatchDate;
      end;
      FindBatchBestBeforeDate(SHrw.ArtCode,SHrw.SerialNr,dat);
      SHrw.BestBefore = dat;
      if ((ORrw.Quant==blankval) and (remn==0)) then begin
        remn = blankval;
      end;
      SHrw.Ordered = ordered;
      if (blank(SHrw.ArtCode)) then begin
        if (SHrw.Ordered==0) then begin
          SHrw.Ordered = blankval;
        end;
      end;
      SHrw.InStock = blankval;
      SHrw.FIFO = blankval;
      SHrw.FIFORowVal = blankval;
      if (nonblank(SHrw.Ordered)) then begin
        SHrw.Ship = SHrw.Ordered;
      end;
      SHrw.BasePrice = ORrw.Price;
      SHrw.BBCostPrice = ORrw.BasePrice;
      SHrw.UnitXval = ORrw.UnitXval;
      SHrw.UnitYval = ORrw.UnitYval;
      SHrw.UnitZval = ORrw.UnitZval;
      if (nonblank(SHrw.ArtCode)) then begin
        res = 0;
      end;    
      MatRowPut(SHp,srw,SHrw);      
      totq = totq + SHrw.Ship;
      shrwcnt = MatRowCnt(SHp);
      NILSHr.CustCode = "NIL";
      if (nonblank(SHrw.ArtCode)) then begin
        if (RefillShipRow(aStockMovr,asmcnt,SHp,NILSHr,INr,srw,shrwcnt,0,aincode,aloc,aserialnr,aqty,acnt)==false) then begin
          MatRowDelete(SHp,srw);
          goto LSKIPROW;
        end;     
      end;
      MatRowGet(SHp,srw,SHrw);
      dontdeletef = true;
      if (IsMaintenance)  then begin
        if (skipzerorows!=0) then begin
//          if (nonblank(SHrw.ArtCode)) then begin
            if (SHrw.Ship<=0) then begin dontdeletef = false; end;
//          end;
        end;
      end;
      if (dontdeletef) then begin
        srw = srw + 1;
        if (MainWHMRec.DemandPosition==0) or (MainWHMRec.Location!=SHp.Location) then begin
          if (INr.SerNrf==1) then begin
            if (SHrw.Ship>0) then begin
              SHrw.Ship = t1;
              SHrw.Ordered = t1;
//              if (MainWHMRec.DemandPosition!=0) then begin//why when demand position only ?
                SHrw.InStock = SHrw.InStock - stock;//done in refillship
//              end;
              if (SHrw.InStock<=0) then begin
                SHrw.Ship = 0;
              end;
              MatRowPut(SHp,srw-1,SHrw);
              remn = remn - t1;
              if (srw>=199) then begin
                if (SaveShipment(aStockMovr,asmcnt,SHp,CUr,ORr,PlanDelRec,totv,totw,totq,srw)==false) then begin
                  goto LPasteOrdInShip;
                end;
              end;
              if (remn>0) then begin
                stock = stock + t1;
                goto L10PasteOrdInShip;
              end;
            end;
          end;
          if ((INr.SerNrf==2) and (mstr.DelivSetBatch==1)) then begin
            if (PasteBatchSerItems(cursernr,curbestbef,srw,remn,ORr,i,SHp,srw-1,mstr.MainStock,mstr.SortBatchOnBestBefore,res)==false) then begin
              goto L10PasteOrdInShip;
            end;
          end;      
        end else begin
          if ((INr.SerNrf==2) and (mstr.DelivSetBatch!=0)) then begin
            srw = srw - 1;
            if (PasteBatchSerItems_DemandPosition(aStockMovr,asmcnt,SHp,srw)==false) then begin
              goto L10PasteOrdInShip;
            end;
          end;
        end;
      end else begin
        if (skipzerorows!=0) then begin
          if (nonblank(SHrw.ArtCode)) then begin
            MatRowDelete(SHp,srw);
          end;
        end;
      end;
      if ((srw>=199) and (i!=(rwcnt-1))) then begin
        if (SaveShipment(aStockMovr,asmcnt,SHp,CUr,ORr,PlanDelRec,totv,totw,totq,srw)==false) then begin
          goto LPasteOrdInShip;
        end;
      end;
    end;
LSKIPROW:;    
  end;
  SetBackOrderComment(SHp);
  SHSumUp(SHp);  
LPasteOrdInShip:;
  PasteOrdInShip = res;
  RETURN;
END;

global
updating function Integer RecordAction_raPasteOrdInShip(var record SHVc SHp,LongInt ordnr,var string warning)
BEGIN
  record SRBlock SRRec;
  Integer res;
  LongInt newnr;
  string 255 tstr;
  Boolean testf;
  Array record StockMovVc aStockMovr;
  Integer asmcnt;
  record ORVc ORr;
  array string 40 aincode;
  array string 40 aloc;
  array string 40 aserialnr;
  array val aqty;
  Integer acnt;
  
  res = -1;
  RecordNew(SHp);
  SHp.SerNr = -1;
  SHp.OrderNr = ordnr;
  if (SHp.SerNr==-1) then begin
    BlockLoad(SRRec);
    newnr = GetCurUserLastNr("SHVc");
    if (newnr!=-1) then begin
      newnr = SRRec.LastShipNr;
    end;
    SHp.SerNr = NextSerNr("SHVc",SHp.ShipDate,newnr,false,"");
    if (SHp.SerNr==-1) then begin
      res = 1747;
      goto LRecordAction_raPasteOrdInShip;
    end;
  end;
  ORr.SerNr = SHp.OrderNr;
  if (ReadFirstMain(ORr,1,true)) then begin end; //## what if not found?

  res = PasteOrdInShip(aStockMovr,asmcnt,ORr,SHp,true,"",0,"",aincode,aloc,aserialnr,aqty,acnt);
  if (EarlierReservationsExists(ORr)) then begin
    warning = USetStr(20562);
  end;
  if ((MatRowCnt(SHp)>0) and (SHp.SerNr!=-1) and (res==0)) then begin
    if (RecordInsert(SHp,false)==false) then begin
      res = 1;
    end else begin
      CreateRecordLink(ORr,CurrentCompany,SHp,CurrentCompany);  
      CreateRecordLink(SHp,CurrentCompany,ORr,CurrentCompany);  
    end;
  end;
LRecordAction_raPasteOrdInShip:;
  RecordAction_raPasteOrdInShip = res;
  RETURN;
END;

global
function Boolean CustOnRestAcc(record RestAccVc RestAccp,record JobVc Jobp)
BEGIN
  row JobVc Jobrw;
  Integer i,rwcnt;
  Boolean res;
  
  if ((RestAccp.CUCode==FindJobCustomerCode(Jobp)) or (RestAccp.CUCode==Jobp.Source)) then begin
    res = true;
  end;  
  if (res==false) then begin
   rwcnt = MatRowCnt(Jobp);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Jobp,i,Jobrw);
      if (RestAccp.CUCode==Jobrw.CUCode) then begin
        res = true;
        i = rwcnt;
      end;
    end;
  end;
  CustOnRestAcc = res;
  RETURN;
END;

procedure GetWaiterForTable(string Mesa,var string ret)
begin
  record NeighbourHoodVc nhr;
  
  ret = "";
  nhr.Table = Mesa;
  if (ReadFirstMain(nhr,1,true)) then begin
    ret = nhr.Waiter;
  end;
  return;
end;

global
procedure RestAccVc_PasteCustCode(var record RestAccVc RestAccr)
begin
  String 20 tstr;

  GetWaiterForTable(RestAccr.CustCode,tstr);
  RestAccr.Waiter2 = tstr;
  return;
end;


global
function Boolean RestAccIsReserved(record RestAccVc RestAccp,var record JobVc Jobp)
BEGIN
  record ReservationStatusVc rsr;
  LongInt resnr;
  Boolean res;
  record HotelBlock Hotelb;

/*
not needed right? 
we just need cuernt resefcation in checkin status
  resnr = FindReservation(RestAccp.Resource,RestAccp.TransDate,RestAccp.TransTime);
  if (resnr<=0) then begin
    goto LRestAccIsReserved;
  end;
  Jobp.SerNr = resnr;
  if (ReadFirstMain(Jobp,1,true)) then  begin
*/  
  BlockLoad(Hotelb);
  Jobp.ResStatus = Hotelb.ChInSt;
  Jobp.ResCode = RestAccp.Resource;
  if (ReadLastKey("ResCodeStatus",Jobp,2,true)) then  begin
    rsr.Code = Jobp.ResStatus;
    if (ReadFirstMain(rsr,1,true)) then begin
      if (rsr.StatType==3) then begin
        res = true;
      end else begin
        Beep;
      end;  
    end else begin
      Beep;
    end;
  end else begin
    Beep;
  end;
LRestAccIsReserved:;  
  RestAccIsReserved = res;
  RETURN;
END;

global
updating function Integer RecordAction_raPasteCOInInv(var record IVVc IVp,var record ORVc ORr,LongInt conr,var Boolean orderf)
BEGIN
  Integer res;
  record RcVc RepSpec;

  RecordNew(IVp);
  RepSpec.f1 = "";
  RepSpec.f2 = conr;
  RepSpec.ArtMode = -1;
  IVp.SerNr = CreateCOInv(RepSpec,false,orderf);
  if (IVp.SerNr!=-1) then begin
    if (orderf) then begin
      ORr.SerNr = IVp.SerNr;
      if (ReadFirstMain(ORr,1,true)) then begin
        res = 1;
      end;
    end else begin
      if (ReadFirstMain(IVp,1,true)) then begin
        res = 1;
      end;
    end;
  end;  
  RecordAction_raPasteCOInInv = res;  
  RETURN;
END;

function Boolean acSetORToInv(var record ORVc ORp)
BEGIN
  Boolean res;
  row ORVc ORrw;
  Integer i,rwcnt;
  
  rwcnt = MatRowCnt(ORp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(ORp,i,ORrw);
    if (ORrw.Invd!=ORrw.Shipd2) then begin
      ORrw.Invd = ORrw.Shipd2;
      MatRowPut(ORp,i,ORrw);
      res = true;
    end;
  end;
  SetORFlags(ORp);  
  acSetORToInv = res;
  RETURN;
END;

global
updating function Boolean RecordAction_rlSetORToInv(var record ORVc ORp)
BEGIN
  Boolean res;
  record ORVc oldORp;
  
  if (IsRecordLocked(ORp)==false) then begin
    if (ReadFirstMain(ORp,0,true)) then begin end;  
    RecordCopy(oldORp,ORp);
    res = acSetORToInv(ORp);
    if (res) then begin
      if (RecordUpdate(oldORp,ORp,false)==0) then begin
      end;
    end;
  end;  
  RecordAction_rlSetORToInv = res;
  RETURN;
END;

global
updating function Integer RecordAction_raPasteQTInProj(var record PRVc PRr,var record QTVc QTp)
BEGIN
  Integer res;
  record PRVc lastPRr;
  string 255 tstr;
  Integer i,rwcnt;
  record QTVc QTr;
  record QTVc oldQTr;
  LongInt prnr;
  record CUVc CUr;
  
  QTr.SerNr = QTp.SerNr;
  if (ReadFirstMain(QTr,1,true)) then begin
    PRr.Code = QTr.PRCode;
    if (ReadFirstMain(PRr,1,true)==false) then begin
      RecordNew(PRr);
      PRr.Code = QTr.PRCode;
      PRr.CustCode = QTr.CustCode;
      PRr.CustName = QTr.Addr0;
      PRr.PrSalesMan = QTr.SalesMan;
      PRr.Leader = QTr.SalesMan;
      GetUserName(PRr.Leader,tstr);
      PRr.LeaderName = tstr;
      PRr.StartDate = CurrentDate;
      PRr.EndDate = CurrentDate;
      PRr.LangCode = QTr.LangCode;
      PRr.Name = QTr.Comment;    
      PRr.Contact = QTr.CustContact;
      PRr.Objects = QTr.Objects;    
	    if (blank(PRr.Contact)) then begin
	      CUr.Code = PRr.CustCode;
	      if (ReadFirstMain(CUr,1,true)) then begin end;
	      PRr.Contact = CUr.Person;
	    end;
      if (blank(PRr.Code)) then begin
        GetNextProjectNr(tstr);
        PRr.Code = tstr;    
        prnr = NextSerNr("PRVc",PRr.StartDate,-1,false,"");
        if (StringToLongInt(PRr.Code)<prnr) then begin
          PRr.Code = prnr;
        end;
      end;
      if (nonblank(PRr.Code)) then begin
        if (RecordStore(PRr,false)) then begin
          res = 1;
        end;
      end;
    end;
  end;
  RecordAction_raPasteQTInProj = res;
  RETURN;
END;

