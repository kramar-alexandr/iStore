external function Integer CheckPORecord(var record POVc,record POVc,LongInt,LongInt,Boolean,var string);
external procedure DivPIFactor(val,val,var val);
external procedure RoundVATSum(string,string,var val);
external function string 255 FindINObjects(string,string);
external procedure PURecalcCost(var record PUVc);
external procedure PUSetShipCost(var record PUVc,Integer);
external function Integer GetPUPControlAcc(string,Integer,string,string,record AccBlock,Integer,var string,var string);
external function Integer GetPUStockAcc(Integer,string,string,string,record AccBlock,Integer,var string,var string);
external procedure NextM4Number(string,var string);
external procedure M4PadString(string,Integer,string,Boolean,var string);
external function string 20 FindFreePositionInLocArea_ExcludePositionFromArray(string,val,record INVc,Array string,Integer);
external procedure CalculatePURowSum(val,val,string,var val,val,
                                     val,val,val,val,val,
                                     string,var val,Boolean,string,
                                     val,val,val,val,val);
external function Boolean GetFirstItem(var string,var record INVc);
external function Integer GetPO(LongInt,var record POVc,string);
external function val DivRateToBase1(string,val,val,val,val,val,val,Integer);
external function val MulRateToBase1(var string,val,val,val,val,val,val,Integer);
external function val FindVAT(string,val,Integer,Integer);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external procedure GetAPAcc(string,var string);
external function Boolean FindItemVAR(string,var string,var string,var string,var string);
external function Boolean GetFirstPurchaseItem(string,string,var record PIVc);
external procedure CurValToOtherCur(Date,string,val,string,var val,Integer);
external procedure PUCalcPerc(val,string,var val);
external procedure PUSumUp(var record PUVc);
external procedure PUCalcCostPrice(string,val,Integer,string,string,
                                   val,val,val,val,val,
                                   val,val,val,val,val,val,
                                   string,var val,val,var val,string,Integer);
external procedure ReadLastINSer(string,var string);
external procedure GetFullCurncyRate (var string,Date,var val,var val,var val,var val,var val);
external procedure GetItemVATCode(string,Integer,var string,Boolean);
external function Boolean GetAccName(string,var string,Integer);
external procedure GetPurchaseControlAcc(string,Integer,string,record AccBlock,string,var string,string);
external procedure MulM4ProcVal(val,val,var val);
external procedure VISumup(record VIVc,var val);
external procedure VICalcVals(var record VIVc);
external procedure VICalcEReb(record VIVc);
external function LongInt DateDiff(Date,Date);

global
procedure NextM4SerialNumber(string code,var string next)
BEGIN
  string 255 pre;
  string 255 suff;
  Integer i,j;
  val t;
  string 1 c;
  string 255 zerostr,tstr;
  Integer padcnt;

  for (i=len(code);i>0;i=i-1) begin
    c = Mid(code,i-1,1);
    if ((asc(c)<asc("0")) or (asc(c)>asc("9"))) then begin//if it is 0, next number for 0123 will be 124
      goto L10NextM4SerialNumber;
    end;
  end;
L10NextM4SerialNumber:;
  for (j=0;j<i;j=j+1) begin
    pre = pre & Mid(code,j,1);
  end;  
  for (j=i;j<=len(code);j=j+1) begin
    suff = suff & Mid(code,j,1);
  end;
  padcnt = len(suff);
  t = StringToVal(suff,M40Val);
  t = t + 1;
//  tstr = Left(t,len(t)-3);
  tstr = ValToString(t,M40Val,"","",0);  
  M4PadString(tstr,padcnt,"0",true,suff);
  next = pre & suff;
  if (next=="0") then begin next = "1"; end;
  if (next=="") then begin next = "1"; end;
  if (nonblank(zerostr)) then begin
    suff = next;
    next = zerostr & suff;
  end;
  RETURN;
END;

function Integer VIRowExists(record VIVc VIp,string accnumber,string vatcode,val rowsump,val q)
BEGIN
  Integer res;
  row VIVc VIrw;
  Integer i,rwcnt;
 
  res = -1;
  rwcnt = MatRowCnt(VIp);
  for (i=0;i<rwcnt;i=i+1) begin  
    MatRowGet(VIp,i,VIrw);
    if ((accnumber==VIrw.AccNumber) and (vatcode==VIrw.VATCode)) then begin
      VIrw.Sum = VIrw.Sum + rowsump;
      VIrw.qty = VIrw.qty + q;
      MatRowPut(VIp,i,VIrw);
      res = i;
      goto LVIRowExists;
    end;
  end;  
LVIRowExists:; 
  VIRowExists = res; 
  RETURN;
END;

function Boolean PUQtyInvoiced(LongInt PUNr,LongInt PURow,LongInt POSerNr,var val puqtyinvd)
begin
  Boolean res;
  record VIVc VIr;
  row VIVc VIrw;
  Integer i,rwcnt;
  Boolean found;
  
  puqtyinvd = blankval;
  if (POSerNr>0) then begin
  found = true;
  VIr.POSerNr = POSerNr;
  while (LoopKey("POSerNr",VIr,1,found)) begin
    if (VIr.POSerNr!=POSerNr) then begin found = false; end;
    if (found) then begin
      rwcnt = MatRowCnt(VIr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(VIr,i,VIrw);
        if (PUNr>0) and (PURow>=0) then begin
          if (VIrw.PUNr==PUNr) and (VIrw.PURow==PURow) then begin
            puqtyinvd = puqtyinvd + VIrw.qty;
            res = true;
          end;
        end;
      end;
    end;
  end;
  end;
  PUQtyInvoiced = res;
  return;
end;

function val FrieghtAlreadyInvoiced(LongInt PUNr,LongInt POSerNr)
begin
  val res;
  record VIVc VIr;
  row VIVc VIrw;
  Integer i,rwcnt;
  Boolean found;
  
  if (POSerNr>0) then begin
  found = true;
  VIr.POSerNr = POSerNr;
  while (LoopKey("POSerNr",VIr,1,found)) begin
    if (VIr.POSerNr!=POSerNr) then begin found = false; end;
    if (found) then begin
      rwcnt = MatRowCnt(VIr);
      for (i=0;i<rwcnt;i=i+1) begin
        MatRowGet(VIr,i,VIrw);
        if (PUNr>0) then begin
          if (VIrw.PUNr==PUNr) then begin
            res = res + VIrw.Sum;
          end;
        end;
      end;
    end;
  end;
  end;
  FrieghtAlreadyInvoiced = res;
  return;
end;

procedure AddFreightRowNew(record PUVc PUp,record VIVc VIp,
                           record AccBlock ARb,record APAccBlock APb,record BaseCurBlock BCb,
                           var Integer nrwcnt)
begin
  row VIVc VIrw;
  string 255 tstr;
  val t,alreadyinvd;

  if (PUp.ShipCost!=0) then begin
    if (GetAccName(ARb.PUFreightAcc,tstr,60)==false) then begin
      goto LAddFreightRowNew;
    end;
    alreadyinvd = FrieghtAlreadyInvoiced(PUp.SerNr,VIp.POSerNr);
    ClearRow(VIp,VIrw,1);
    VIrw.AccNumber = ARb.PUFreightAcc;
    VIrw.Comment = tstr;
    if (ARb.BookPriceVar!=0) then begin
      VIrw.OrdRow = -8;
    end;

    VIrw.qty = PUp.SumQuant;
    VIrw.PUNr = PUp.SerNr;
    VIrw.Objects = "";
    VIrw.Sum = PUp.ShipCost;    
    switch (VIp.ExportFlag) begin
      case 0: VIrw.VATCode = APb.VATCodeDom;
      case 1: VIrw.VATCode = APb.VATCodeEU;
      case 3: VIrw.VATCode = APb.VATCodeEU;
      case 2: VIrw.VATCode = APb.VATCodeExp;
      case 4: VIrw.VATCode = APb.VATCodeExp;
    end;
    if (BCb.BaseCur1!=VIp.CurncyCode) then begin
      CurValToOtherCur(PUp.TransDate,BCb.BaseCur1,VIrw.Sum,VIp.CurncyCode,t,DefaultCurRoundOff);
      VIrw.Sum = t;
    end;
    VIrw.Sum = VIrw.Sum - alreadyinvd;
    if (VIrw.Sum>0) then begin
      MatRowPut(VIp,nrwcnt,VIrw);
      nrwcnt = nrwcnt + 1;
    end;
  end;  
LAddFreightRowNew:;
  return;
end;

procedure AddFreightRow(record PUVc PUp,row PUVc PUrw,Integer purow,record VIVc VIp,
                        record AccBlock ARb,record APAccBlock APb,record BaseCurBlock BCb,
                        var Integer nrwcnt)
BEGIN
  row VIVc VIrw;
  string 255 tstr;
  val t;
  
  if (PUrw.ShipCost!=0) then begin
    if (GetAccName(ARb.PUFreightAcc,tstr,60)==false) then begin
      goto LAddFreightRow;
    end;
    ClearRow(VIp,VIrw,1);
    VIrw.AccNumber = ARb.PUFreightAcc;
    VIrw.Comment = tstr;
    if (ARb.BookPriceVar!=0) then begin
      VIrw.OrdRow = -8;
    end else begin
      VIrw.Item = PUrw.ArtCode;
      VIrw.OrdRow = PUrw.OrdRow;
    end;
//      VIrw.PONr = PUp.PONr;

//    VIrw.qty = PUrw.Quant;, qty can not be here
    VIrw.PUNr = PUp.SerNr;
    VIrw.PURow = purow;
    VIrw.Objects = PUrw.Objects;
    VIrw.Sum = PUrw.Quant*PUrw.ShipCost;    
    switch (VIp.ExportFlag) begin
      case 0: VIrw.VATCode = APb.VATCodeDom;
      case 1: VIrw.VATCode = APb.VATCodeEU;
      case 3: VIrw.VATCode = APb.VATCodeEU;
      case 2: VIrw.VATCode = APb.VATCodeExp;
      case 4: VIrw.VATCode = APb.VATCodeExp;
    end;
    if (BCb.BaseCur1!=VIp.CurncyCode) then begin
      CurValToOtherCur(PUp.TransDate,BCb.BaseCur1,VIrw.Sum,VIp.CurncyCode,t,DefaultCurRoundOff);
      VIrw.Sum = t;
    end;
    MatRowPut(VIp,nrwcnt,VIrw);
    nrwcnt = nrwcnt + 1;
  end;
LAddFreightRow:;  
  RETURN;
END;

global
updating function LongInt PastePUInVI(record PUVc PUp,var record VIVc VIp,var string errmsg)
BEGIN
  LongInt res;
  Integer i,rwcnt;
  row VIVc VIrw;
  row PUVc PUrw;  
  Integer nrwcnt,virow;
  record INVc INr;
  val net,sum,t,q;
  record CUVc VEr;
  record APAccBlock APb;
  record AccBlock ARb;
  record CostAccBlock CostAccRec;
  record BaseCurBlock BCb;
  record VITBlock VITb;
  val from,to1,to2,base1,base2;
  string 255 tstr,viitgroup;
  val purowsum,puqinvd;
  record POVc POr;
  record POVc rPOr;
  record POVc oldPOr;
  row POVc POrw;
  Boolean vifound;

  res = 0;
  BlockLoad(BCb);
  BlockLoad(ARb);
  BlockLoad(APb);
  BlockLoad(VITb);
  BlockLoad(CostAccRec);  
  VEr.Code = PUp.VECode;
  if (ReadFirstMain(VEr,1,true)==false) then begin 
    res = 1434;
    goto LPastePUInVI; 
  end;
  if (PUp.PONr>0) then begin
    POr.SerNr = PUp.PONr;
    if (ReadFirstMain(POr,1,true)==false) then begin 
      res = 1434;
      goto LPastePUInVI; 
    end else begin
      res = CheckPORecord(POr,oldPOr,1,1,false,errmsg);
      if (res!=0) then begin
        goto LPastePUInVI; 
      end;
    end;
  end;
  GetAPAcc(VEr.Code,tstr);
  VIp.APAcc = tstr;
  VIp.POCQStatNr = -1;
  VIp.POSerNr = PUp.PONr;
  VIp.TransDate = PUp.TransDate;
  VIp.InvDate = PUp.TransDate;  
  VIp.VECode = PUp.VECode;
  VIp.VEName = PUp.VEName;
  VIp.CurncyCode = PUp.CurncyCode;
  VIp.LangCode = VEr.LangCode;
  VIp.PRCode = VEr.VEInvoiceToCode;
  VIp.BankAcc = VEr.BankAccount;
  VIp.BankCode = VEr.AccOperator;
  VIp.SortCode = VEr.SortCode;
  VIp.VATNr = VEr.VATNr;
  VIp.POCQStatNr = -1;
  VIp.PayDeal = "";
  VIp.FrRate = PUp.FrRate;
  VIp.ToRateB1 = PUp.ToRateB1;
  VIp.ToRateB2 = PUp.ToRateB2;
  VIp.BaseRate1 = PUp.BaseRate1;
  VIp.BaseRate2 = PUp.BaseRate2;
  VIp.NoTAXonVAT = PUp.NoTAXonVAT;
  if ((APb.UpdBaseRate!=0) or (APb.UpdForeginRate!=0)) then begin
    GetFullCurncyRate(VIp.CurncyCode,VIp.TransDate,from,to1,to2,base1,base2);
    if (APb.UpdBaseRate!=0) then begin
      VIp.BaseRate1 = base1;
      VIp.BaseRate2 = base2;
    end;
    if (APb.UpdForeginRate!=0) then begin
      VIp.FrRate = from;
      VIp.ToRateB1 = to1;
      VIp.ToRateB2 = to2;
    end;
  end;
  VIp.PayDeal = POr.PayDeal;
  if (blank(VIp.PayDeal)) then begin 
    VIp.PayDeal = VEr.VEPayDeal;
  end;
  VIp.ExportFlag = VEr.ExportFlag;
  VICalcEReb(VIp);
  if (APb.APUseObj!=0) then begin
    VIp.Objects = VEr.VEObjects;
  end;
  rwcnt = MatRowCnt(PUp);
  nrwcnt = 0;
  ClearRow(VIp,VIrw,1);
  for (i=0;i<rwcnt;i=i+1) begin  
    MatRowGet(PUp,i,PUrw);
    vifound = PUQtyInvoiced(PUp.SerNr,i,PUp.PONr,puqinvd);
    PUrw.Quant = PUrw.Quant - puqinvd;

    q = PUrw.Quant;
    if ((PUrw.PONr>0) and (PUrw.OrdRow>=0)) then begin
      VIrw.PONr = PUrw.PONr;
      VIrw.OrdRow = PUrw.OrdRow;
      if (vifound==false) then begin
        rPOr.SerNr = PUrw.PONr;
        if (ReadFirstMain(rPOr,1,true)) then begin 
          if (PUrw.OrdRow>=0) then begin
            MatRowGet(rPOr,PUrw.OrdRow,POrw);
            if (q>(POrw.Quant-POrw.Invd)) then begin
              q = POrw.Quant-POrw.Invd;
            end;
          end;
        end;
      end;
      if (q>0) then begin 
        VIrw.Item = PUrw.ArtCode;
        VIrw.qty = q;
        VIrw.PUNr = PUp.SerNr;
        VIrw.PURow = i;
        VIrw.Objects = PUrw.Objects;
        VIrw.SerialNr = PUrw.SerialNr;
      end;
    end else begin
      if (vifound==false) then begin
        if (PUrw.OrdRow>=0) then begin
          MatRowGet(POr,PUrw.OrdRow,POrw);
          if (q>(POrw.Quant-POrw.Invd)) then begin
            q = POrw.Quant - POrw.Invd;
          end;
        end;
      end;
      if (q>0) then begin 
        VIrw.OrdRow = -2; // Row comes from Order, but is not connected to a specific row 
        if (VITb.TransferItems==2) or (PUrw.StockType==kStockTypeConsigment) then begin
          VIrw.Item = PUrw.ArtCode;
          VIrw.qty = q;
          VIrw.OrdRow = PUrw.OrdRow;
          VIrw.PUNr = PUp.SerNr;
          VIrw.PURow = i;
          VIrw.Objects = PUrw.Objects;
          VIrw.SerialNr = PUrw.SerialNr;//??
        end else begin
          VIrw.qty = q;
          virow = VIRowExists(VIp,VIrw.AccNumber,VIrw.VATCode,PUrw.Sum,q);
          if (virow==-1) then begin
            if (nonblank(VIrw.AccNumber)) then begin
              MatRowPut(VIp,nrwcnt,VIrw);
              VIrw.PUNr = PUp.SerNr;
              nrwcnt = nrwcnt + 1;
            end;
          end else begin
            goto LSKIPROW;
          end;
        end;
      end;      
    end;
    
    VIrw.VATCode = PUrw.VATCode;
    VIrw.StockType = PUrw.StockType;
    if (blank(VIrw.VATCode)) then begin
      VIrw.VATCode = VEr.VEVATCode;
    end;
    if (blank(VIrw.VATCode)) then begin
      GetItemVATCode(PUrw.ArtCode,VEr.ExportFlag,tstr,false);
      VIrw.VATCode = tstr;
    end;  
    GetPurchaseControlAcc(PUrw.CredAcc,CostAccRec.ItemGroupAccounts,PUrw.ArtCode,ARb,VEr.AccCost,tstr,PUp.VECode);
    VIrw.AccNumber = tstr;
    if (GetAccName(VIrw.AccNumber,tstr,60)) then begin end;
    VIrw.Comment = tstr;    
    purowsum = PUrw.Quant*PUrw.UPrice;
    if (PUrw.PONr>0) or (PUp.PONr>0) then begin
      purowsum = PUrw.Quant*PUrw.UPrice;
    end;
    sum = FindVAT(VIrw.VATCode,purowsum,PUp.InclVAT,PUp.NoTAXonVAT);
    if (PUp.InclVAT!=0) then begin
      sum = purowsum - sum;
    end else begin
      sum = purowsum;
    end;  
    VIrw.Sum = Round(sum,DefaultCurRoundOff);
    net = net + VIrw.Sum;
    
    if (nonblank(VIrw.Item)) then begin
      if (blank(viitgroup)) then begin
        if (ReadFirstItem(VIrw.Item,INr,true,true)) then begin
          viitgroup = INr.Group;
        end;
      end;    
    end;
    if (VIrw.Sum!=0) then begin
      MatRowPut(VIp,nrwcnt,VIrw);
      nrwcnt = nrwcnt + 1;         
    end;
LSKIPROW:;    
  end;  
  AddFreightRowNew(PUp,VIp,ARb,APb,BCb,nrwcnt);
  VICalcVals(VIp);
  VISumup(VIp,t);
  VIp.PayVal = -t;
  VIp.PayVal = Round(VIp.PayVal,DefaultCurRoundOff);
  if (VITb.autoVATflag!=0) then begin
    if (nonblank(VITb.autoVATVal)) then begin
      if ((VIp.ExportFlag==0) or (VIp.ExportFlag==3) or (VIp.ExportFlag==4)) then begin
        MulM4ProcVal(StringToVal(VITb.autoVATVal,M4Rate),VIp.PayVal,t);
        RoundVATSum(VIp.CurncyCode,VIp.PayDeal,t);      
        VIp.VATVal = t;
      end;
    end;
  end;
/*
external function string 20 AssignAcceptanceGroup(string,string,val);
  VIp.AcceptanceGroup = AssignAcceptanceGroup("VIVc",viitgroup,VIp.PayVal);  
*/
  if (nonblank(VEr.VEWarnText1)) then begin 
    MessageBox(0,VEr.VEWarnText1);
  end;
LPastePUInVI:;
  PastePUInVI = res;
  RETURN;
END;

function string 255 ReadLastSerialNr(record PUVc PUp,string artcode,Integer currow)
begin
  string 255 res;
  row PUVc PUrw;
  Integer i;

  for (i=currow-1;i>=0;i=i-1) begin
    MatRowGet(PUp,i,PUrw);
    if (PUrw.ArtCode==artcode) then begin
      if (nonblank(PUrw.SerialNr)) then begin
        res = PUrw.SerialNr;
        goto LReadLastSerialNr;
      end;
    end;
  end;
LReadLastSerialNr:;  
  ReadLastSerialNr = res;
  return;
end;

procedure CalcEANCHS(var string str)
begin
integer c1,c2,lenth,i,chsum,litle;
val sum;
  sum = 0;
  lenth = len(str);
  if (lenth==12)then begin
    for(i=0;i<12;i=i+2)begin
      c1 = evaltoval(mid(str,i,1));
      c2 = evaltoval(mid(str,i+1,1));
      c2=c2*3;
      sum = sum+c1+c2;
    end;
    //StopAlert(sum);
    if(round(sum/10,0)>sum/10) then begin
      litle = round(sum/10,0)-1;
    end else begin
      litle = round(sum/10,0);
    end;
    chsum  = 10-(10*(sum/10-litle));
    if(chsum==10) then begin chsum = 0; end;
    //StopAlert(chsum);
    str = str & chsum;
  end else begin
    str = "";
  end;
  
return;
end;

global
procedure AutoFillPUSerNr(var record PUVc PUp)// Edit ************************** понедельник, 21 июня 2010 г. 15:42:45
BEGIN
  Integer i,rwcnt,lastno,days,altlen,li;
  row PUVc PUrw,chPUrw;
  string 255 lastnr,lastitem,serial;
  string 5 daysstr,lastnostr,year;
  record INVc INr;
  record ItemHistVc IHr;
  
  year = getyear(PUp.TransDate);
  days=DateDiff(PUp.TransDate,"1/1/" & getyear(PUp.TransDate));
  
  //StopAlert(days);
  rwcnt = MatRowCnt(PUp);
  if (rwcnt==0) then begin goto LAutoFillPUSerNr; end;
  
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PUp,i,PUrw);
    ReadFirstItem(PUrw.ArtCode,INr,true,true);
    
    if(nonblank(PUrw.SerialNr))then begin
      goto L33AutoFillPUSerNr;
    end;
    
    if (INr.SerNrf==0) then begin
      goto L33AutoFillPUSerNr;
    end;
    
    if (blank(INr.AlternativeCode)) then begin
      goto L33AutoFillPUSerNr;
    end;
    
    altlen = len(INr.AlternativeCode);
    if (altlen!=5) then begin
      goto L33AutoFillPUSerNr;
    end;
    
    for(li=0;li<altlen;li=li+1) begin
      if((mid(INr.AlternativeCode,li,1)>"9") or (mid(INr.AlternativeCode,li,1))<"0") then begin
        goto L33AutoFillPUSerNr;
      end;
    end;
    //if(lastno == i) then begin
      //lastno = lastno+1;
    //end;
    L44AutoFillPUSerNr:;
    lastnostr = lastno;
    if (lastno<100) then begin lastnostr = "0" & lastnostr; end;
    if (lastno<10) then begin lastnostr = "0" & lastnostr; end;
    daysstr = days;
    if (days<100) then begin daysstr = "0" & daysstr; end;
    if (days<10) then begin daysstr = "0" & daysstr; end;
    
    serial = INr.AlternativeCode & daysstr & mid(year,3,1) & lastnostr;
    CalcEANCHS(serial);
    // Edit ************************** вторник, 22 июня 2010 г. 09:32:16
    IHr.ArtCode = INr.Code;
    IHr.SerialNr = serial;
    if(ReadFirstKey("ArtCodeSerialNr",IHr,2,true)) then begin
      //i=i-1;
      lastno = lastno+1;
      if (lastno>999) then begin
        goto L33AutoFillPUSerNr;
      end;
      goto L44AutoFillPUSerNr;
    end else begin
      lastno = lastno+1;
      PUrw.SerialNr = serial;
      MatRowPUT(PUp,i,PUrw);
    end;
    // Edit ************************** вторник, 22 июня 2010 г. 09:32:28
    L33AutoFillPUSerNr:;
  end;
  
  /*for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PUp,i,PUrw);
    ReadFirstItem(PUrw.ArtCode,INr,true,true);
    if (INr.SerNrf==0) then begin
      goto L33AutoFillPUSerNr;
    end;
    if (blank(lastitem)) then begin
      lastitem = PUrw.ArtCode;
    end;
    if (i==0) then begin
      if (blank(PUrw.SerialNr)) then begin
        ReadLastINSer(PUrw.ArtCode,lastnr);
      end else begin
        lastnr = PUrw.SerialNr;
        goto L33AutoFillPUSerNr;
      end;
    end;
    if ((blank(PUrw.SerialNr)) and (PUrw.ArtCode==lastitem)) then begin
      NextM4Number(lastnr,lastnr);
      PUrw.SerialNr = lastnr;
    end else begin
      lastnr = PUrw.SerialNr;
      lastitem = PUrw.ArtCode;
      if (blank(PUrw.SerialNr)) then begin
        lastnr = ReadLastSerialNr(PUp,PUrw.ArtCode,i);
        if (blank(lastnr)) then begin
          ReadLastINSer(PUrw.ArtCode,lastnr);
        end;
      end else begin
        lastnr = PUrw.SerialNr;
        goto L33AutoFillPUSerNr;
      end;
      NextM4SerialNumber(lastnr,lastnr);      
      PUrw.SerialNr = lastnr;
    end;
    MatRowPut(PUp,i,PUrw);
L33AutoFillPUSerNr:;
  end;*/
LAutoFillPUSerNr:;
  RETURN;
END;

global
procedure FindPUStockAcc(string vecode,record CostAccBlock CostAccRec,string rowstockacc,string rowcontrolacc,string loccode,record INVc INr,Integer stocktype,var string stockacc,var string purchacc)
BEGIN
  record AccBlock ARb;
  string 255 tstr;
  record LocationVc Locr;

  BlockLoad(ARb);
  Locr.Code = loccode;
  if (ReadFirstMain(Locr,1,true)) then begin end;
  if (GetPUStockAcc(CostAccRec.ItemGroupAccounts,rowstockacc,Locr.StockAcc,INr.Group,ARb,stocktype,stockacc,tstr)==0) then begin end;  
  if (GetPUPControlAcc(vecode,CostAccRec.ItemGroupAccounts,rowcontrolacc,INr.Group,ARb,stocktype,purchacc,tstr)==0) then begin end;
  RETURN;
END;

global
function Boolean PUVc_PasteArtCode(var record PUVc PUr,Integer rownr,var string warning,var string inwarn)
BEGIN
  Boolean res;
  record CostAccBlock CostAccRec;
  record PIVc PIr;
  record INVc INr;
  row PUVc PUrw;
  Boolean chsum,chrsum;
  string 60 sz,msk,mskrep,pcstr,tstr;
  val t,p,s,t2;
  record SysFormatBlock SysFormatRec;
  string 200 varsubset;
  Boolean infoundf;
  record RoundBlock RoundRec;
  string 20 stockacc,purchacc;
  
  MatRowGet(PUr,rownr,PUrw);    
  infoundf = GetFirstItem(PUrw.ArtCode,INr);
  if (infoundf==false) then begin
    infoundf = ReadFirstItem(PUrw.ArtCode,INr,true,true);
    if (FindItemVAR(INr.Code,sz,msk,mskrep,varsubset)) then begin INr.Code = PUrw.ArtCode; end;
  end;
  if (infoundf) then begin
    BlockLoad(SysFormatRec);
    BlockLoad(CostAccRec);    
    BlockLoad(RoundRec);    
    if (nonblank(INr.WarnText1)) then begin
      inwarn = INr.WarnText1;
    end;
    if (INr.ItemType!=1) then begin
      warning = USetStr(1301);
    end;
    pcstr = "";
    if (GetFirstPurchaseItem(PUrw.ArtCode,PUr.VECode,PIr)) then begin
      if (PIr.CurncyCode==PUr.CurncyCode) then begin
        PUrw.UPrice = PIr.PurPrice;
        p = PIr.PurPrice;
      end else begin
        CurValToOtherCur(PUr.TransDate,PIr.CurncyCode,PIr.PurPrice,PUr.CurncyCode,t,RoundRec.RndFIFO);
        PUrw.UPrice = t;  
        p = PUrw.UPrice;
      end;          
      PUrw.CustomsCost = PIr.PurchaseCost;
      PUrw.VEItemCode = PIr.VEItemCode;
      PUrw.VEUnit = PIr.VEUnit;
    end else begin
      PUrw.Spec = INr.Name;
      PUrw.UPrice = INr.InPrice;
      p = INr.InPrice;
      if (INr.PriceFactor!=0) then begin
        p = p/INr.PriceFactor;
      end;
      p = DivRateToBase1(PUr.CurncyCode,p,PUr.FrRate,PUr.ToRateB1,PUr.ToRateB2,PUr.BaseRate1,PUr.BaseRate2,RoundRec.RndFIFO);
    end; 
    PUrw.VATCode = PUr.VEVATCode;    
    if (blank(PUrw.VATCode)) then begin
      GetItemVATCode(PUrw.ArtCode,PUr.ExportFlag,tstr,false);
      PUrw.VATCode = tstr;
    end;
    PUrw.Coefficient = INr.UnitCoefficient;
    PUrw.InPrice = INr.InPrice;
    PUrw.ArtCode = INr.Code;
    PUrw.Spec = INr.Name;
    PUrw.Objects = FindINObjects(INr.Objects,INr.Group);
    PUrw.UPrice = p;
    PUrw.UnitXval = INr.Width;
    PUrw.UnitYval = INr.Height;
    PUrw.UnitZval = INr.Depth;
    PUrw.UnitCode = INr.Unittext;
    PUrw.Quant=1;

    t2 = MulRateToBase1(PUr.CurncyCode,p,PUr.FrRate,PUr.ToRateB1,PUr.ToRateB2,PUr.BaseRate1,PUr.BaseRate2,RoundRec.RndFIFO);
    PUCalcPerc(t2,PUrw.CustomsCost,t2);
    pcstr = ValToString(t2,M45Val,SysFormatRec.thousSep,SysFormatRec.decimalPt,0);
    PUrw.CustomsCost = pcstr;
    PUrw.BasePrice = INr.UPrice1;
    tstr = INr.ExtraCost;
    PUrw.Extra = tstr;
    PUrw.PIFactor = PIr.PIFactor;
    if (PUrw.PIFactor!=0) then begin
      PUrw.UPrice = PUrw.UPrice/PUrw.PIFactor;
//      PUrw.BasePrice = PUrw.BasePrice/PUrw.PIFactor;
//BasePrice effected by Supplier Unit ? 
    end;
    DivPIFactor(PUrw.Quant,PUrw.PIFactor,t);
    PUrw.VEQuant = t;
    
    PUrw.CostAcc = "";
    PUrw.CredAcc = "";
    if (blank(PUrw.Location)) then begin
      FindPUStockAcc(PUr.VECode,CostAccRec,PUrw.CostAcc,PUrw.CredAcc,PUr.Location,INr,PUrw.StockType,stockacc,purchacc);
    end else begin
      FindPUStockAcc(PUr.VECode,CostAccRec,PUrw.CostAcc,PUrw.CredAcc,PUrw.Location,INr,PUrw.StockType,stockacc,purchacc);
    end;
    PUrw.CostAcc = stockacc;
    PUrw.CredAcc = purchacc;
    MatRowPut(PUr,rownr,PUrw);
    chrsum = true;
    res = true;
  end;
  if (chrsum) then begin
    PUCalcCostPrice(PUrw.ArtCode,PUrw.UPrice,PUr.InclVAT,PUrw.Extra,PUr.CurncyCode,
                    PUr.FrRate,PUr.ToRateB1,PUr.ToRateB2,PUr.BaseRate1,PUr.BaseRate2,
                    PUrw.ShipCost,PUrw.RowCost1,PUrw.RowCost2,PUrw.RowCost3,PUrw.RowCost4,PUrw.RowCost5,
                    PUrw.CustomsCost,p,PUrw.Quant,s,PUrw.VATCode,PUr.ExportFlag);
    PUrw.CostPrice = p;
    PUrw.Sum = s;
    chsum = true;
  end;
  MatRowPut(PUr,rownr,PUrw);    
  if (chsum) then begin
    PUSumUp(PUr);
  end;
LPUVc_PasteArtCode:; 
  PUVc_PasteArtCode = res;
  RETURN;
END;
      
global
procedure PUVc_InclVATButtonAction(var record PUVc PUp)
BEGIN    
  row PUVc PUrw;
  val bas,s;
  Integer i,rwcnt;
  
  rwcnt = MatRowCnt(PUp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PUp,i,PUrw);
    PUCalcCostPrice(PUrw.ArtCode,PUrw.UPrice,PUp.InclVAT,PUrw.Extra,PUp.CurncyCode,
                    PUp.FrRate,PUp.ToRateB1,PUp.ToRateB2,PUp.BaseRate1,PUp.BaseRate2,
                    PUrw.ShipCost,PUrw.RowCost1,PUrw.RowCost2,PUrw.RowCost3,PUrw.RowCost4,PUrw.RowCost5,
                    PUrw.CustomsCost,bas,PUrw.Quant,s,PUrw.VATCode,PUp.ExportFlag);
    PUrw.CostPrice = bas;
    PUrw.Sum = s;
    MatRowPut(PUp,i,PUrw);
  end;
  PUSumUp(PUp);
  RETURN;
END;
      
global
function Integer PUVc_PastePONr(var record PUVc PUp,Integer rownr)
BEGIN
  row PUVc PUrw;
  record POVc POr;
  Integer retval;
  
  MatRowGet(PUp,rownr,PUrw);
  if (PUrw.PONr>0) then begin
    retval = GetPO(PUrw.PONr,POr,PUp.VECode);
  end;
  PUVc_PastePONr = retval;
  RETURN;
END;

procedure AddToSkiposArray(string poscode,var Array string askippos,var Integer acnt)
BEGIN
  row PUVc PUrw;
  Integer i;
  
  if (blank(poscode)) then begin goto LAddToSkiposArray; end;
  for (i=0;i<acnt;i=i+1) begin
    if (askippos[i]==poscode) then begin
      goto LAddToSkiposArray;
    end;
  end;
  askippos[acnt] = poscode; 
  acnt = acnt + 1;
LAddToSkiposArray:;  
  RETURN;
END;

procedure PrepareSkiposArray(record PUVc PUr,var Array string askippos,var Integer acnt)
BEGIN
  row PUVc PUrw;
  Integer i,rwcnt;
  
  rwcnt = MatRowCnt(PUr);
  for (i=0;i<rwcnt-1;i=i+1) begin
    MatRowGet(PUr,i,PUrw);
    AddToSkiposArray(PUrw.PosCode,askippos,acnt);
    AddToSkiposArray(PUrw.ToPosCode,askippos,acnt);
  end;
  RETURN;
END;

global
function Boolean IsQtyAllowed_QtyonPallet(val Quant,string artcode)
BEGIN
  Boolean res;
  record INVc INr;
  Boolean itemfound;
  
  res = true;
  itemfound = GetFirstItem(artcode,INr);
  if (itemfound==false) then begin
    itemfound = ReadFirstItem(artcode,INr,true,false);
  end;
  if ((itemfound) and (INr.QtyonPallet>0)) then begin
    if ((Quant/INr.QtyonPallet)>78) then begin
      res = false;
    end;
  end;
  IsQtyAllowed_QtyonPallet = res;
  RETURN;
END;

global
function Integer PUVc_PasteQuant(var record PUVc PUr,Integer rownr)
BEGIN
  Integer res,i;
  row PUVc PUrw,orgPUrw;
  row PUVc palletPUrw;
  val p,s;
  Boolean chsum,testf;
  record MainStockBlock MainStockRec;
  record MainWHMBlock MainWHMRec;
  record INVc INr;
  val orgquant;
  string 255 tstr,tstr2;
  Integer lrownr;
  Array string 20 askippos;
  Integer acnt;
  record LocationVc LocRec;
  val qtyonpallet,defposhight;
  
  LocRec.Code = PUr.Location;
  if (blank(LocRec.Code)) then begin 
    BlockLoad(MainStockRec);
    LocRec.Code = MainStockRec.MainStock;
  end;
  BlockLoad(MainWHMRec);
  if (ReadFirstMain(LocRec,1,true)) then begin end;  
  res = 0;      
  lrownr = rownr;
  MatRowGet(PUr,lrownr,PUrw);
  CopyRow(PUr,PUrw,orgPUrw);
  orgquant = orgPUrw.Quant;
LPUVc_PasteQuantAGAIN:;  
  if (LocRec.RequirePos!=0) then begin    
    PrepareSkiposArray(PUr,askippos,acnt);
    if (ReadFirstItem(PUrw.ArtCode,INr,true,true)) then begin
      qtyonpallet = INr.QtyonPallet + (INr.QtyonPallet*20)/100;
      if (nonblank(INr.DefPalletItem)) then begin
        if (INr.QtyonPallet>0) then begin
          if ((PUrw.Quant/INr.QtyonPallet)>100) then begin 
            res = 1733;
            goto LPUVc_PasteQuant;
          end;
          if (PUrw.Quant>INr.QtyonPallet) then begin
            if (PUrw.Quant<=qtyonpallet) then begin
              if (PUrw.Quant>=qtyonpallet) then begin
                PUrw.Quant = qtyonpallet;
              end;
            end else begin
              PUrw.Quant = INr.QtyonPallet;
            end;
          end;
        end;
      end;
    end;
  end;
  DivPIFactor(PUrw.Quant,PUrw.PIFactor,p);
  PUrw.VEQuant = p;
  CalculatePURowSum(PUrw.Quant,PUrw.UPrice,PUrw.Extra,PUrw.CostPrice,PUrw.ShipCost,PUrw.RowCost1,PUrw.RowCost2,PUrw.RowCost3,PUrw.RowCost4,PUrw.RowCost5,
                    PUrw.CustomsCost,p,false,PUr.CurncyCode,PUr.FrRate,PUr.ToRateB1,PUr.ToRateB2,PUr.BaseRate1,PUr.BaseRate2);

  PUrw.Sum = p;
  chsum = true;
  if (blank(PUrw.PosCode)) then begin 
    if (LocRec.Code==MainWHMRec.Location) then begin
      PUrw.PosCode = MainWHMRec.DefPUPosCode; 
    end;
  end;
  if (LocRec.RequirePos!=0) then begin    
    if (blank(PUrw.ToPosCode)) then begin
      defposhight = INr.DefPosHeight;
      if (defposhight==0) then begin
        defposhight = INr.PalletHeight + 3; 
      end;
      PUrw.ToPosCode = FindFreePositionInLocArea_ExcludePositionFromArray(INr.LocArea,defposhight,INr,askippos,acnt);
    end;
  end;
  MatRowPut(PUr,lrownr,PUrw);    
  if (LocRec.RequirePos!=0) then begin
    if (nonblank(INr.DefPalletItem)) then begin
      lrownr = lrownr + 1;
      ClearRow(PUr,palletPUrw,1);
      palletPUrw.PosCode = PUrw.PosCode;
      palletPUrw.ToPosCode = PUrw.ToPosCode;
      palletPUrw.ArtCode = INr.DefPalletItem;
      palletPUrw.Quant = 1;
      MatRowInsert(PUr,lrownr,palletPUrw); 
      testf = PUVc_PasteArtCode(PUr,lrownr,tstr,tstr2);       
    end;
    lrownr = lrownr + 1;
    orgquant = orgquant - PUrw.Quant;
    if (orgquant>0) then begin
      CopyRow(PUr,orgPUrw,PUrw);
      PUrw.Quant = orgquant;
      goto LPUVc_PasteQuantAGAIN;
    end;
  end;    
LPUVc_PasteQuant:; 
  PUSetShipCost(PUr,2);
  PURecalcCost(PUr);
  PUSumUp(PUr);
  PUVc_PasteQuant = res;
  RETURN;
END;

global
procedure RefillPURows(var record PUVc PUp)
BEGIN
  Integer rwcnt,i;
  Integer oldrwcnt;
  row PUVc PUrw;
  row PUVc newPUrw;
  record INVc INr;
  record MainStockBlock MSb;
  val remn,pv,sv;
  Integer nrwcnt;
  Boolean firstf;
  
  BlockLoad(MSb);
  rwcnt = MatRowCnt(PUp);
  for (i=0;i<rwcnt;i=i+1) begin
    oldrwcnt = MatRowCnt(PUp);
    MatRowGet(PUp,i,PUrw);
    if (ReadFirstItem(PUrw.ArtCode,INr,true,true)) then begin
      if (nonblank(INr.DefPalletItem)) then begin        
        if (INr.QtyonPallet>0) then begin
          if ((PUrw.Quant/INr.QtyonPallet)>100) then begin
            MessageBox(1733,"");
          end else begin    
            if (PUVc_PasteQuant(PUp,i)==0) then begin end;
            rwcnt = MatRowCnt(PUp);
            i = i + (rwcnt - oldrwcnt);
          end;
        end;
      end;
    end;
  end;

  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(PUp,i,PUrw);
    if ((MSb.NoSerOnPU==0) and (PUrw.Quant>1)) then begin
	    if (ReadFirstItem(PUrw.ArtCode,INr,true,true)) then begin
		    if (INr.SerNrf==1) then begin
		      firstf = true;
		      nrwcnt = i;
		      remn = PUrw.Quant;
		      while (remn>0) begin
		        if (rwcnt>=199) then begin
		          goto LRefillPURows;
		        end;
		        CopyRow(PUp,PUrw,newPUrw);
		        
		        newPUrw.Quant = 1;
		        remn = remn - 1;
            if (CharacterMatch(newPUrw.CustomsCost,"%",len(newPUrw.CustomsCost))==false) then begin
  		        newPUrw.CustomsCost = StringToVal(newPUrw.CustomsCost,M4Val)/PUrw.Quant;
            end;
            if (CharacterMatch(newPUrw.ShipCost,"%",len(newPUrw.ShipCost))==false) then begin
  		        newPUrw.ShipCost = StringToVal(newPUrw.CustomsCost,M4Val)/PUrw.Quant;
            end;
		        newPUrw.RowCost1 = newPUrw.RowCost1/PUrw.Quant;
		        newPUrw.RowCost2 = newPUrw.RowCost2/PUrw.Quant;
		        newPUrw.RowCost3 = newPUrw.RowCost3/PUrw.Quant;
		        newPUrw.RowCost4 = newPUrw.RowCost4/PUrw.Quant;
		        newPUrw.RowCost5 = newPUrw.RowCost5/PUrw.Quant;		        

		        PUCalcCostPrice(PUrw.ArtCode,newPUrw.UPrice,PUp.InclVAT,newPUrw.Extra,PUp.CurncyCode,
		                        PUp.FrRate,PUp.ToRateB1,PUp.ToRateB2,PUp.BaseRate1,PUp.BaseRate2,
		                        newPUrw.ShipCost,newPUrw.RowCost1,newPUrw.RowCost2,newPUrw.RowCost3,newPUrw.RowCost4,newPUrw.RowCost5,
		                        newPUrw.CustomsCost,pv,newPUrw.Quant,sv,newPUrw.VATCode,PUp.ExportFlag);
		        newPUrw.CostPrice = pv;                                        
		        newPUrw.Sum = sv;		      
		        if (firstf) then begin
  		        MatRowPut(PUp,nrwcnt,newPUrw);
  		        firstf = false;
  		      end else begin
  		        MatRowInsert(PUp,nrwcnt,newPUrw);
  		      end;
		        nrwcnt = nrwcnt + 1;
		      end;
		      rwcnt = MatRowCnt(PUp);
		      i = nrwcnt;
		    end;
	    end;
	  end;
  end;
LRefillPURows:;  
	PUSumUp(PUp);
  RETURN;
END;

