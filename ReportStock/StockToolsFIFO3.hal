external function val FindCostPriceBasePrice(record CostAccBlock,record INVc,record ITVc);
external function val QtyUsedBeforeDate(record ItemHistVc,Boolean,Date);
external inner function val ConsigmentStockQty(string,string,Date);
external inner procedure GetCostsFromRecords(record ItemHistVc,var val,var val,Boolean,Date);
external function val FindWAPriceBackdated(Integer,record INVc,string,val,Date,Boolean,Integer);
external inner function val GetStockQty(string,string,Date,Boolean);
external function val FindCostPriceFIFO(Integer,record INVc,string,string,val,val,Boolean,Boolean,Boolean,Integer,Integer);
external inner function Boolean TestArtCode(string,string,record INVc);
external inner function Integer CostPriceDecimals();
external inner function val CalculateQty2(val,val);
external inner function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external updating procedure StoreItemHistory_In(record MainStockBlock,string,string,string,string,Date,Date,val,val,val,LongInt,Integer,Integer,string,string);
external function val FindCostPriceBasePrice(record CostAccBlock,record INVc,record ITVc);
external procedure GetCostModel(record INVc,record CostAccBlock,var record ITVc,var Integer,var Integer,Boolean);
external inner function Integer ItemCostPerLocation(record INVc);
external function val FindCostPrice(string,string,string,val,val,Boolean);
external inner function Integer ArtCodeCostPerLocation(string);
external updating procedure StoreItemHistory_In_WithSource(record MainStockBlock,string,string,string,string,Date,Date,val,val,val,val,LongInt,Integer,Integer,LongInt,string);
external inner updating procedure StoreItemHistory_In_RetGoods_InQue(record MainStockBlock,string,string,string,string,Date,Date,val,val,val,LongInt,Integer,Integer,string,LongInt);
external inner procedure FindItemName(string,var string);
global
function Boolean ConsigmentStock(string item,string serialnr,Date td)
begin
  Boolean res,found;
  record BatchTextVc btr;
  record VIVc VIr;
  row VIVc VIrw;
  Integer i,rwcnt;
  string 255 keystr;
  
  btr.ArtCode = item;
  btr.SerialNr = serialnr;
  if (ReadFirstMain(btr,2,true)) then begin
    res = btr.ConsigStock!=0;
  end;
  if (res) then begin
    if (nonblankdate(td)) then begin
      keystr = "Item:" & item;
      VIr.TransDate = td;
      found = true;
      while (LoopBackKey(keystr,VIr,1,found)) begin
        if (VIr.TransDate>td) then begin found = false; end;
        if (found) then begin
          rwcnt = MatRowCnt(VIr);
          for (i=0;i<rwcnt;i=i+1) begin
            MatRowGet(VIr,i,VIrw);
            if (VIrw.SerialNr==serialnr) then begin
              res = 0;
              goto LConsigmentStock;
            end;
          end;
        end;
      end;
    end;
  end;
LConsigmentStock:;  
  ConsigmentStock = res;
  return;
end;

global
function Integer WAPerLocation(record INVc INr,record ITVc ITr,record CostAccBlock CAb)
begin
  Integer res;
  
  switch (INr.WAPerLocation) begin
    case 0:
      if (CAb.ItemGroupCostModel!=0) then begin
        switch (ITr.WAPerLocation) begin
          case 0: ;
          otherwise
            res = ITr.WAPerLocation;
        end;
      end;
    otherwise
      res = INr.WAPerLocation;
  end;  
  if (res==0) then begin
    res = CAb.WAPerLocation;
  end else begin
    res = res - 1;
  end;
  WAPerLocation = res;
  return;
end;

global
updating procedure RetStoreItemHist_In_InQue_Comp(record MainStockBlock MSb,record CostAccBlock CAb,record RetVc Retp,row RetVc Retrw,
                                                  Integer rownr,row SHVc RecSHrw,Integer stockaffectf,
                                                  string location)
begin
  record ItemHistVc IHr;
  record ItemHistVc sourceIHr;
  record ItemHistVc oldsourceIHr;
  Boolean foundf,testf;
  Date fifod;
  val cost;

  foundf = true;
  IHr.FileName = "SHVc";
  IHr.TransNr = Retp.SHNr;
  IHr.Row = Retrw.SHRow;
  while (LoopKey("FNTransNr",IHr,3,foundf)) begin
    if (IHr.FileName!="SHVc") then begin foundf = false; end;
    if (IHr.TransNr!=Retp.SHNr) then begin foundf = false; end;
    if (IHr.Row!=Retrw.SHRow) then begin foundf = false; end;
    if (foundf) then begin
      testf = true;
      if (IHr.Source<=0) then begin testf = false; end;
      if (IHr.ArtCode!=RecSHrw.ArtCode) then begin testf = false; end;      
      if (IHr.Invalid!=0) then begin testf = false; end;
      if (testf) then begin
        sourceIHr.SerNr = IHr.Source;
        if (ReadFirstMain(sourceIHr,1,true)) then begin
          fifod = sourceIHr.FIFODate;                
          cost = (IHr.TotCostPrice/IHr.Qty)*RecSHrw.Ship;
          if (cost<0) then begin cost = -cost; end;
          if (ArtCodeCostPerLocation(RecSHrw.ArtCode)!=0) then begin
            if (location!=sourceIHr.Location) then begin
              fifod = Retp.TransDate;
              StoreItemHistory_In_WithSource(MSb,RecSHrw.ArtCode,"",location,"RetVc",Retp.TransDate,fifod,RecSHrw.Ship,RecSHrw.Coefficient,cost,blankval,Retp.SerNr,rownr,stockaffectf,IHr.Source,Retrw.PosCode);
              StepForward(IHr,1);
            end else begin
              RecordCopy(oldsourceIHr,sourceIHr);
              sourceIHr.RemQty = sourceIHr.RemQty + RecSHrw.Ship;
              if (RecordUpdate(oldsourceIHr,sourceIHr,false)) then begin end;
              StoreItemHistory_In_RetGoods_InQue(MSb,RecSHrw.ArtCode,"",location,"RetVc",Retp.TransDate,Retp.TransDate,RecSHrw.Ship,RecSHrw.Coefficient,cost,Retp.SerNr,rownr,stockaffectf,Retrw.PosCode,IHr.SerNr);
              StepForward(IHr,1);
            end;
          end else begin
            RecordCopy(oldsourceIHr,sourceIHr);
            sourceIHr.RemQty = sourceIHr.RemQty + RecSHrw.Ship;
            if (RecordUpdate(oldsourceIHr,sourceIHr,false)) then begin end;
            StoreItemHistory_In_RetGoods_InQue(MSb,RecSHrw.ArtCode,"",location,"RetVc",Retp.TransDate,Retp.TransDate,RecSHrw.Ship,RecSHrw.Coefficient,cost,Retp.SerNr,rownr,stockaffectf,Retrw.PosCode,IHr.SerNr);
            StepForward(IHr,1);
          end;
        end;
      end;
    end;
  end;
  return;
end;

global
function val ItemHist_ComponentItem_CostPrice(record CostAccBlock CAb,string filename,LongInt transnr,LongInt rownr,string artcode,string location)
begin
  val res;
  val qty;
  record ItemHistVc IHr;
  Boolean foundf,cosfound,testf;

  if (CAb.RetCostPriceFromSH!=0) then begin
    if (IHr.Row>=0)  then begin
      foundf = true;
      IHr.FileName = filename;
      IHr.TransNr = transnr;
      IHr.Row = rownr;
      while (LoopKey("FNTransNr",IHr,3,foundf)) begin
        if (IHr.FileName!=filename) then begin foundf = false; end;
        if (IHr.TransNr!=transnr) then begin foundf = false; end;
        if (IHr.Row!=rownr) then begin foundf = false; end;
        testf = true;
        if (IHr.Invalid!=0) then begin testf = false; end;
        if (foundf and testf) then begin
          if (IHr.ArtCode==artcode) then begin
            res = res + IHr.TotCostPrice;
            qty = qty + IHr.Qty;
          end;
        end;
      end;
      if (res<0) then begin res = -res; end;
      if (qty<0) then begin qty = -qty; end;
      res = res/qty;
      cosfound = true;
    end;
  end;
  if (cosfound==false) then begin
    res = FindCostPrice(artcode,location,"",1,0,false);
  end;
  ItemHist_ComponentItem_CostPrice = res;
  RETURN;
END;

global
function val FindCostPriceWA(Integer WAPerLocation,record INVc INr,string location)
begin
  val res;
  record WeigAvVc WAr;

  res = 0;
  if (WAPerLocation!=0) then begin
    WAr.ArtCode = INr.Code;//ISVC
    WAr.Location = location;
    if (ReadFirstMain(WAr,2,true)) then begin
      res = WAr.WeighedAvPrice;
    end;
  end else begin
    res = INr.WeighedAvPrice;//ISVC
  end;
  FindCostPriceWA = res;
  return;
end;

global
function val FindCostPrice_ReturnedGoods(record RetVc Retr,row RetVc Retrw)
begin
  val res;
  record ItemHistVc IHr;
  record ItemHistVc sourceIHr;
  record ItemHistVc oldsourceIHr;
  Boolean foundf,testf;
  Date fifod;
  val cost;
  val qty,remqty,sq;

  remqty = Retrw.Quant;
  foundf = true;
  IHr.FileName = "SHVc";
  IHr.TransNr = Retr.SHNr;
  IHr.Row = Retrw.SHRow;
  while (LoopKey("FNTransNr",IHr,3,foundf)) begin
    if (IHr.FileName!="SHVc") then begin foundf = false; end;
    if (IHr.TransNr!=Retr.SHNr) then begin foundf = false; end;
    if (IHr.Row!=Retrw.SHRow) then begin foundf = false; end;
    if (remqty<=0) then begin foundf = false; end;
    testf = foundf;
    if (IHr.Invalid!=0) then begin testf = false; end;
    if (testf) then begin
      if (IHr.Source>0) then begin
        sourceIHr.SerNr = IHr.Source;
        if (ReadFirstMain(sourceIHr,1,true)) then begin
          sq = sourceIHr.Qty - sourceIHr.RemQty;
          if (remqty>sq) then begin
            qty = sq;
          end else begin
            qty = remqty;
          end;
          res = res + ((sourceIHr.TotCostPrice-sourceIHr.RemCostPrice)/sq)*qty;
          remqty = remqty - qty;
        end;
      end;
    end;
  end;
  FindCostPrice_ReturnedGoods = res;
  RETURN;
END;

global
function val FindCostPrice_ReturnedGoods_FromSH(record INVc INr,record RetVc Retr,row RetVc Retrw,Integer currow)
begin
  val res;
  record SHVc SHr;
  row SHVc SHrw;
  
  SHr.SerNr = Retr.SHNr;
  if (ReadFirstMain(SHr,1,true)) then begin
    if (Retrw.SHRow>=0) then begin
      MatRowGet(SHr,Retrw.SHRow,SHrw);
      res = SHrw.FIFORowVal/SHrw.Ship;
    end else begin
      res = INr.WeighedAvPrice;
    end;
  end else begin    
  end;
  FindCostPrice_ReturnedGoods_FromSH = res;
  RETURN;
END;

global
function val FindProdOperationsCost_FinalProduct(LongInt prodnr,LongInt MaxSequence,LongInt skipprodopnr)
begin
  val res;
  record ProdOperationVc ProdOpr;
  row ProdOperationVc ProdOprw;
  Integer i,rwcnt;
  Boolean found,testf;
  val actualqty;
  record ProdSettingsBlock PSb;
  
  BlockLoad(PSb);
  found = true;
	ProdOpr.ProdNr = prodnr;
	ProdOpr.PRStatusFlag = 3;
	while (LoopKey("ProdNr",ProdOpr,2,found)) begin
	  if (ProdOpr.ProdNr!=prodnr) then begin found = false; end;
	  if (ProdOpr.PRStatusFlag!=3) then begin found = false; end;
    if (found) then begin
      testf = true;
      if (ProdOpr.SerNr==skipprodopnr) then begin testf = false; end;
  	  if (ProdOpr.Sequence>MaxSequence) then begin testf = false; end;
      if (testf) then begin
			  rwcnt = MatRowCnt(ProdOpr);
			  for (i=0;i<rwcnt;i=i+1) begin
			    MatRowGet(ProdOpr,i,ProdOprw);
			    if (ProdOprw.InQty!=0) then begin
				    switch (ProdOpr.RowsHoldActualQty) begin
				      case 1: res = res + ProdOprw.InQty*ProdOprw.ItemCost;
				      otherwise res = res + ProdOprw.InQty*ProdOprw.ItemCost*ProdOpr.Qty;
				    end;				    
				    actualqty = actualqty + ProdOpr.ActualQty;
			    end;
			  end;
		  end;
		end;
  end;
  if (PSb.AddDiscardedCost!=0) then begin
	  ResetLoop(ProdOpr);
	  found = true;
		ProdOpr.ProdNr = prodnr;
		ProdOpr.PRStatusFlag = 5;
		while (LoopKey("ProdNr",ProdOpr,2,found)) begin
		  if (ProdOpr.ProdNr!=prodnr) then begin found = false; end;
		  if (ProdOpr.PRStatusFlag!=5) then begin found = false; end;
	    if (found) then begin
	      testf = true;
	      if (ProdOpr.SerNr==skipprodopnr) then begin testf = false; end;
	  	  if (ProdOpr.Sequence>MaxSequence) then begin testf = false; end;
	      if (testf) then begin
				  rwcnt = MatRowCnt(ProdOpr);
				  for (i=0;i<rwcnt;i=i+1) begin
				    MatRowGet(ProdOpr,i,ProdOprw);
				    if (ProdOprw.InQty!=0) then begin
					    switch (ProdOpr.RowsHoldActualQty) begin
					      case 1: res = res + ProdOprw.InQty*ProdOprw.ItemCost;
					      otherwise res = res + ProdOprw.InQty*ProdOprw.ItemCost*ProdOpr.Qty;
					    end;		      
					    actualqty = actualqty + ProdOpr.ActualQty;
				    end;
				  end;
			  end;
			end;
	  end;
	end;
  FindProdOperationsCost_FinalProduct = res;
  RETURN;
END;

global
function val FindProdOperationsFinishedCost_FinalProduct(LongInt prodnr,LongInt TheSequence,LongInt skipprodopnr,var val finqty)
begin
  val res;
  record ProdOperationVc ProdOpr;
  row ProdOperationVc ProdOprw;
  Integer i,rwcnt;
  Boolean found,testf;
  val inv,outv;
  
  finqty = blankval;
  found = true;
	ProdOpr.ProdNr = prodnr;
	ProdOpr.PRStatusFlag = 3;
	while (LoopKey("ProdNr",ProdOpr,2,found)) begin
	  if (ProdOpr.ProdNr!=prodnr) then begin found = false; end;
	  if (ProdOpr.PRStatusFlag!=3) then begin found = false; end;
    if (found) then begin
      testf = true;
      if (ProdOpr.SerNr==skipprodopnr) then begin testf = false; end;
  	  if (ProdOpr.Sequence!=TheSequence) then begin testf = false; end;
      if (testf) then begin
			  rwcnt = MatRowCnt(ProdOpr);
			  for (i=0;i<rwcnt;i=i+1) begin
			    MatRowGet(ProdOpr,i,ProdOprw);
			    if (ProdOprw.InQty!=0) then begin
				    switch (ProdOpr.RowsHoldActualQty) begin
				      case 1: inv = inv + ProdOprw.InQty*ProdOprw.ItemCost;
				      otherwise inv = inv + ProdOprw.InQty*ProdOprw.ItemCost*ProdOpr.Qty;
				    end;				    
			    end;
			    if (ProdOprw.OutQty!=0) then begin
				    switch (ProdOpr.RowsHoldActualQty) begin
				      case 1: outv = outv + ProdOprw.OutQty*ProdOprw.ItemCost;
				      otherwise outv = outv + ProdOprw.OutQty*ProdOprw.ItemCost*ProdOpr.Qty;
				    end;				    
			    end;			   
			  end;
			  finqty = finqty + ProdOpr.ActualQty;
		  end;
		end;
  end;
  res = outv - inv;
  FindProdOperationsFinishedCost_FinalProduct = res;
  RETURN;
END;

function Boolean FindSourceFIFO(record MainStockBlock MSb,record CostAccBlock CAb,record INVc INr,string location,string serialnr,var record ItemHistVc sourceIHr)
begin
  Boolean res;
  string 200 thekey;
  Integer keys;
  Boolean foundf;
  
  res = false;
  sourceIHr.ArtCode = INr.Code;
  sourceIHr.Location = location;
  if (ItemCostPerLocation(INr)==0) then begin
    thekey = "ActiveQty";
    keys = 1;
  end else begin
    thekey = "ActiveLocQty";
    keys = 2;
  end;
  ResetLoop(sourceIHr);
  foundf = true;
  while (LoopKey(thekey,sourceIHr,keys,foundf)) begin
    if (sourceIHr.ArtCode!=INr.Code) then begin
      foundf = false;
    end;
    if (keys==2) then begin
      if (sourceIHr.Location!=location) then begin
        foundf = false;
      end;
    end;
    if (sourceIHr.RemQty<0) then begin
      if ((blank(sourceIHr.SerialNr)) and (blank(sourceIHr.Variety))) then begin // If a normal stocked item has a record with negative RemQty, there is no need to read further
        foundf = false;
      end;
    end;
    if (foundf) then begin
      if (sourceIHr.Qty>0) then begin
        res = true;
        goto LFindSourceFIFO;
      end;
    end;
  end;
LFindSourceFIFO:;
  FindSourceFIFO = res;
  return;
end;

function Boolean FindSourceSerialNr(record MainStockBlock MSb,record CostAccBlock CAb,record INVc INr,string location,string serialnr,var record ItemHistVc sourceIHr)
begin
  Boolean res;
  string 200 thekey;
  Integer keys;
  Boolean foundf;
  
  res = false;
  sourceIHr.ArtCode = INr.Code;
  sourceIHr.Location = location;
  if (ItemCostPerLocation(INr)==0) then begin
    thekey = "ActiveQty";
    keys = 1;
  end else begin
    thekey = "ActiveLocQty";
    keys = 2;
  end;
  ResetLoop(sourceIHr);
  foundf = true;
  while (LoopKey(thekey,sourceIHr,keys,foundf)) begin
    if (sourceIHr.ArtCode!=INr.Code) then begin
      foundf = false;
    end;
    if (keys==2) then begin
      if (sourceIHr.Location!=location) then begin
        foundf = false;
      end;
    end;
    if (foundf) then begin
      if (sourceIHr.SerialNr==serialnr) then begin
        if (sourceIHr.Qty>0) then begin
          res = true;
          goto LFindSourceSerialNr;
        end;
      end;
    end;
  end;
LFindSourceSerialNr:;
  FindSourceSerialNr = res;
  return;
end;

function Boolean FindSourceLIFO(record MainStockBlock MSb,record CostAccBlock CAb,record INVc INr,string location,string serialnr,var record ItemHistVc sourceIHr)
begin
  Boolean res;
  string 200 thekey;
  Integer keys;
  Boolean foundf;
  
  res = false;
  sourceIHr.ArtCode = INr.Code;
  sourceIHr.Location = location;
  if (ItemCostPerLocation(INr)==0) then begin
    thekey = "ActiveQty";
    keys = 1;
  end else begin
    thekey = "ActiveLocQty";
    keys = 2;
  end;
  ResetLoop(sourceIHr);
  foundf = true;
  while (LoopBackKey(thekey,sourceIHr,keys,foundf)) begin
    if (sourceIHr.ArtCode!=INr.Code) then begin
      foundf = false;
    end;
    if (keys==2) then begin
      if (sourceIHr.Location!=location) then begin
        foundf = false;
      end;
    end;
    if (sourceIHr.RemQty<0) then begin
      if ((blank(sourceIHr.SerialNr)) and (blank(sourceIHr.Variety))) then begin // If a normal stocked item has a record with negative RemQty, there is no need to read further
        foundf = false;
      end;
    end;
    if (foundf) then begin
      if (sourceIHr.Qty>0) then begin
        res = true;
        goto LFindSourceLIFO;
      end;
    end;
  end;
LFindSourceLIFO:;
  FindSourceLIFO = res;
  return;
end;

function Boolean FindSource(record MainStockBlock MSb,record CostAccBlock CAb,record INVc INr,string location,string serialnr,var record ItemHistVc sourceIHr)
begin
  Integer primary,queuecostmodel;
  Boolean res;
  record ITVc ITr;
  
  RecordNew(sourceIHr);
  GetCostModel(INr,CAb,ITr,primary,queuecostmodel,true);
  res = false;
  switch (queuecostmodel) begin
    case 0:
      res = FindSourceFIFO(MSb,CAb,INr,location,serialnr,sourceIHr);
    case 1:
      res = FindSourceLIFO(MSb,CAb,INr,location,serialnr,sourceIHr);
    case 20: 
      res = FindSourceSerialNr(MSb,CAb,INr,location,serialnr,sourceIHr);
  end;
  FindSource = res;
  return;
end;

global
updating procedure StoreItemHistory_Out(record MainStockBlock MSb,string artcode,string serialnr,string location,string filename,Date td,val qty,val coeff,val totalcost,LongInt transnr,Integer rownr,Integer stockaffectf,string position)
begin
  record ItemHistVc IHr;
  record ItemHistVc sourceIHr;
  record ItemHistVc oldsourceIHr;
  record INVc INr;
  record ITVc ITr;
  LongInt sourcenr;
  val t,rem,remsourceqty,totsourceqty,totsourcecost,useqty;
  Boolean foundf;
  record CostAccBlock CAb;
  Integer primary,queuecostmodel;
  val ltotalcost;
  Date blankd;

  ltotalcost = totalcost;//JJ->KH waht do u think about it ?, this is cause of structure items, do we need flag or it can belike this and this what happens with ltotalcost
  if (blank(artcode)) then begin 
    goto LStoreItemHistory_Out;
  end;
  if (qty<0) then begin // for negative item lines, not allowed, but still exists in old data
    if (ltotalcost<0)  then begin
      StoreItemHistory_In(MSb,artcode,serialnr,location,filename,td,td,-qty,coeff,-ltotalcost,transnr,rownr,stockaffectf,position,"");
    end else begin
      StoreItemHistory_In(MSb,artcode,serialnr,location,filename,td,td,-qty,coeff,ltotalcost,transnr,rownr,stockaffectf,position,"");
    end;
    goto LStoreItemHistory_Out;
  end;
  BlockLoad(CAb);
  rem = qty;
  while (rem>0) begin
    RecordNew(IHr);
    IHr.ArtCode = artcode;
    IHr.Variety = "";
    IHr.StockAffectf = stockaffectf;
    if (ReadFirstItem(artcode,INr,false,false)) then begin
      if (artcode!=INr.Code) then begin
        IHr.Variety = right(artcode,len(artcode)-len(INr.Code));
        IHr.ArtCode = INr.Code;
      end;
      switch (INr.ItemType) begin
        case 0:
          switch (filename) begin
            case "SHVc": ;
            otherwise IHr.StockAffectf = 0;
          end;
        case 1: ;
        case 2:
          if (nonblank(INr.Recepy)) then begin
            IHr.StockAffectf = 0;
          end;
        case 3: IHr.StockAffectf = 0;
      end;
    end;
    IHr.SerialNr = serialnr;
    IHr.Location = location;
    IHr.FileName = filename;
    IHr.TransDate = td;
    IHr.FIFODate = td;
    IHr.RemQty = BlankVal;
    if (IHr.StockAffectf!=0) then begin
      foundf = FindSource(MSb,CAb,INr,location,serialnr,sourceIHr);
      if (ItemCostPerLocation(INr)==0) then begin
        if (filename=="StockMovVc") then begin
          IHr.FIFODate = sourceIHr.FIFODate;
          if (blankdate(IHr.FIFODate)) then begin
            IHr.FIFODate = td;
          end;
        end;
      end;
    end;
    if (foundf) then begin
      if (rem>sourceIHr.RemQty) then begin
        useqty = sourceIHr.RemQty;
      end else begin
        useqty = rem;
      end;
    end else begin
      useqty = rem;
      if ((IHr.StockAffectf!=0) and ((INr.ItemType==1) or (INr.ItemType==2))) then begin
        IHr.RemQty = -useqty;
      end;
    end;
    if (ConsigmentStock(IHr.ArtCode,IHr.SerialNr,blankd)) then begin
      IHr.ConsigStock = 1;
    end;
    IHr.Qty = -useqty;
    IHr.Qty2 = CalculateQty2(IHr.Qty,coeff);
    IHr.TransNr = transnr;
    IHr.Row = rownr;
    if (foundf) then begin
      if (sourceIHr.RemQty==useqty) then begin
        IHr.TotCostPrice = sourceIHr.RemCostPrice;
      end else begin
        IHr.TotCostPrice = (sourceIHr.TotCostPrice/sourceIHr.Qty) * useqty;
      end;
      IHr.Source = sourceIHr.SerNr;
    end else begin
      if (IHr.RemQty<0) then begin
//        IHr.TotCostPrice = (ltotalcost/qty)*useqty;
        IHr.TotCostPrice = ltotalcost - totsourcecost;
      end else begin
        IHr.TotCostPrice = ltotalcost;
      end;
      IHr.Source = -1;
    end;

    if (ltotalcost==0) then begin
      ltotalcost = IHr.TotCostPrice;
    end;

    switch (filename) begin
      case "RetPUVc":
        if (CAb.RetPUCostPriceNotFromPU!=0) then begin
          IHr.TotCostPrice = ltotalcost;
        end;
    end;

    ITr.Code = INr.Group;
    if (ReadFirstMain(ITr,1,true)) then begin
    end;
    GetCostModel(INr,CAb,ITr,primary,queuecostmodel,false);
    IHr.VECode = sourceIHr.VECode;
//    IHr.TotCostPrice = Round(IHr.TotCostPrice,CostPriceDecimals);
    IHr.TotCostPrice = Round(IHr.TotCostPrice,CostPriceDecimals);
    IHr.WATotCost = -IHr.Qty*INr.WeighedAvPrice;
    IHr.WATotCostPerLoc = -IHr.Qty*FindCostPriceWA(1,INr,location);
    IHr.CPTotCost = -IHr.Qty*INr.InPrice;
//  IHr.CPTotCostPerLoc = -IHr.Qty*;
    IHr.BPTotCost = -IHr.Qty*FindCostPriceBasePrice(CAb,INr,ITr);
//  IHr.BPTotCostPerLoc = -IHr.Qty*;
/*
    switch (primary) begin
      case 1:
        IHr.CPTotCost = (ltotalcost/qty)*(-IHr.Qty);
//        IHr.CPTotCostPerLoc = (ltotalcost/qty)*(-IHr.Qty);
      case 2:
        IHr.BPTotCost = (ltotalcost/qty)*(-IHr.Qty);
//        IHr.BPTotCostPerLoc = (ltotalcost/qty)*(-IHr.Qty);
      case 3:
        IHr.WATotCost = (ltotalcost/qty)*(-IHr.Qty);
        IHr.WATotCostPerLoc = (ltotalcost/qty)*(-IHr.Qty);
    end;
*/    
    IHr.Position = position;
    IHr.SerNr = NextSerNr("ItemHistVc",IHr.TransDate,-1,false,"");
    if (RecordStore(IHr,false)) then begin end;
    if (foundf) then begin
      RecordCopy(oldsourceIHr,sourceIHr);
      sourceIHr.RemQty = sourceIHr.RemQty - useqty;
      sourceIHr.RemCostPrice = sourceIHr.RemCostPrice - IHr.TotCostPrice;
      totsourcecost = totsourcecost + IHr.TotCostPrice;
      if (RecordUpdate(oldsourceIHr,sourceIHr,false)==0) then begin end;
    end;
    rem = rem - useqty;
  end;
LStoreItemHistory_Out:;
  return;
end;

// Edit Start ---------------------------------------------- Edit Start
	//Wednesday, 17 March 2010 4:41:53 PM
	global
procedure PrintItemHistLine(record ItemHistVc IHr,val totcostprice)
BEGIN
  string 255 tstr;
  string 255 itemname;
  record RLinkVc RLr;
// Edit Start ---------------------------------------------- Edit Start
	//Friday, 29 October 2010 12:49:29
	  Integer notenr,mtws,z;
  record ORVc ORr;
  row ORVc ORrw;
  record StockMovVc SMr;
  row StockMovVc SMrw;
  boolean trhs1;
  record INVc INr;
  record ITVc ITr;
	// Edit End ---------------------------------------------- Edit End
	
  
  switch (IHr.FileName) begin
    case "IVVc": tstr = USetStr(1150);
    case "PUVc": tstr = USetStr(1157);
    case "ProdVc": tstr = USetStr(1162);
    case "RetVc": tstr = USetStr(1421);
    case "StockMovVc": tstr = USetStr(1172);
    case "StockTakeVc": tstr = USetStr(1392);
    case "ProdOperationVc": tstr = USetStr(1633);
    case "SRVc": tstr = USetStr(1636);
  end;
  tstr = tstr & ":";
  tstr = tstr & IHr.TransNr;
  FindItemName(IHr.ArtCode,itemname);
  StartFormat(15);
  OutString(10,0,IHr.ArtCode,false);
  OutString(10,0,itemname,false);
  OutString(90,"DblItemHistLine",tstr,false);
  OutString(180,0,IHr.SerialNr,false);
  
  OutDate(260,0,IHr.TransDate,false);
  OutVal(3,0,IHr.RemQty,M4Qty,true);
  OutString(5,0,IHr.Location,false);
 // Edit Start ---------------------------------------------- Edit Start
	//Friday, 29 October 2010 12:51:40
  if(UserCanAction("ViewCostPrice",true))then begin
    OutVal(4,0,totcostprice/IHr.RemQty,M45Val,true);
    OutVal(1,0,totcostprice,M45Val,true);
   
  
    if(IHr.FileName=="StockMovVc") then begin
      trhs1=true;
      notenr = 1;
      SMr.SerNr = IHr.TransNr;
      if(ReadFirstMain(SMr,1,true)) begin
        while (ReadRecordLink(SMr,notenr,ORr,RLr) and trhs1) begin
          mtws = matrowcnt(ORr);
          for(z=0;z<mtws;z=z+1) begin
            MatRowGet(ORr,z,ORrw);
            if(ORrw.ArtCode==IHr.ArtCode and trhs1)then begin
              OutString(1,0,ORr.CustCode,true);
              OutVal(1,0,ORrw.Price,M45Val,true);
              trhs1 = false;
            end;
          end;
          notenr=notenr+1;
        end;
      end;
      if(trhs1)then begin
        OutString(1,0,"",true);
        OutString(1,0,"",true);
      end;
    end else begin
      OutString(1,0,"",true);
      OutString(1,0,"",true);
    end;
  end;
	// Edit End ---------------------------------------------- Edit End
	INr.Code = IHr.ArtCode;
	Readfirstmain(INr,1,true);
	OutString(1,0,INr.Group,true);
  ITr.Code = INr.Group;
  readfirstmain(ITr,1,true);
  OutString(1,0,ITr.Comment,true);
  
  EndFormat;
  return;
end;
	// Edit End ---------------------------------------------- Edit End
	

/*
global
function val FindCostPriceCostPrice(Integer CostPricePerLocation,record INVc INr,string location)
begin
  val res;
  record ItemHistVc ItemHistr;
  Boolean found;

  res = 0;
  if (CostPricePerLocation!=0) then begin
    ItemHistr.ArtCode = INr.Code;
    ItemHistr.Location = location;
    if (ReadLastKey("ArtCodeLoc",ItemHistr,2,false)) begin    
      found = true;
      if (ItemHistr.ArtCode!=INr.Code) then begin found = false; end;
      if (ItemHistr.Location!=location) then begin found = false; end;
      if (found) then begin
        res = ItemHistr.CPTotCostPerLoc;//ISVC
      end;
    end;
  end else begin
    res = INr.InPrice;//ISVC
  end;
  FindCostPriceCostPrice = res;
  return;
end;
*/

global
updating procedure StockMovStoreItemHistory_In(record MainStockBlock MSb,string artcode,string serialnr,string frlocation,string location,string filename,Date td,val qty,val coeff,val unitcost,LongInt transnr,Integer rownr,Integer stockaffectf,string topos)
begin
  record ItemHistVc IHr;
  record ItemHistVc newIHr;
  Boolean foundf,ihf;
  record CostAccBlock CAb;
  Date fifod;

  BlockLoad(CAb);
  foundf = true;
  IHr.FileName = "StockMovVc";
  IHr.TransNr = transnr;
  IHr.Row = rownr;
  while (LoopKey("FNTransNr",IHr,3,foundf)) begin
    if (IHr.FileName!="StockMovVc") then begin foundf = false; end;
    if (IHr.TransNr!=transnr) then begin foundf = false; end;
    if (IHr.Row!=rownr) then begin foundf = false; end;
    if (foundf) then begin
//      if (IHr.Source>0) and (IHr.Qty<0) and (IHr.Location==frlocation) then begin
      if (IHr.Qty<0) and (IHr.Location==frlocation) then begin
        fifod = IHr.FIFODate;
        if (ArtCodeCostPerLocation(artcode)!=0) then begin
          fifod = IHr.TransDate;
        end;
//        StoreItemHistory_In(MSb,artcode,IHr.SerialNr,location,filename,IHr.TransDate,fifod,-IHr.Qty,coeff,IHr.TotCostPrice,transnr,rownr,stockaffectf,topos,"");
        StoreItemHistory_In(MSb,artcode,IHr.SerialNr,location,filename,IHr.TransDate,fifod,-IHr.Qty,coeff,-IHr.Qty*unitcost,transnr,rownr,stockaffectf,topos,"");
        ihf = true;
      end;
    end;
  end;
  if (ihf==false) and (ISMAINTENANCE) then begin
    StoreItemHistory_In(MSb,artcode,serialnr,location,"StockMovVc",td,td,qty,coeff,qty*unitcost,transnr,rownr,stockaffectf,topos,"");
  end;
  return;
end;

procedure CalculateINTransferRowFIFO(record CostAccBlock CostAccRec,string location,record INTransferVc INTransferp,row INTransferVc INTransferrw,Integer currow)
BEGIN
  val adjstockv,t;
  row INTransferVc INTransfer2rw;
  Integer j;
  record INVc INr;

  if (ReadFirstItem(INTransferrw.ArtCode,INr,true,false)) then begin end;
  adjstockv = 0;
  for (j=0;j<currow;j=j+1) begin  
    MatRowGet(INTransferp,j,INTransfer2rw);
    if (TestArtCode(INTransfer2rw.ArtCode,INTransferrw.ArtCode,INr)) then begin
      adjstockv = adjstockv + INTransfer2rw.Quant;
    end;
  end;
  INTransferrw.FIFORowVal = FindCostPrice(INTransferrw.ArtCode,"",INTransferrw.SerialNr,INTransferrw.Quant,adjstockv,false);
  INTransferrw.FIFORowVal = Round(INTransferrw.FIFORowVal,5);
  INTransferrw.FIFO = INTransferrw.FIFORowVal/INTransferrw.Quant;
  RETURN;
END;

global
procedure UpdateINTransferFIFO(record INTransferVc INTransferp)
BEGIN
  record MainStockBlock MainStockRec;
  record CostAccBlock CostAccRec;
  row INTransferVc INTransferrw;
  row INTransferVc INTransfer2rw;
  Integer i,rwcnt,j;
  val t,adjstockv;
  string 10 location;
  string 10 loc2;
  
  BlockLoad(MainStockRec);
  BlockLoad(CostAccRec);
  location = INTransferp.FrLocation;
  if (blank(location)) then begin
    location = MainStockRec.MainStock;
  end;
  rwcnt = MatRowCnt(INTransferp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(INTransferp,i,INTransferrw);
    loc2 = location;
    if (nonblank(INTransferrw.ArtCode)) then begin
      if (INTransferrw.Quant!=0) then begin
        if ((INTransferrw.Quant!=0) and (INTransferrw.FIFO==0)) then begin          
          CalculateINTransferRowFIFO(CostAccRec,loc2,INTransferp,INTransferrw,i);
        end else begin
          INTransferrw.FIFORowVal = INTransferrw.Quant*INTransferrw.FIFO;
          INTransferrw.FIFORowVal = Round(INTransferrw.FIFORowVal,5);
        end;
        MatRowPut(INTransferp,i,INTransferrw);
      end;
    end;
  end;
  return;
end;

function Boolean SerialNrCheck(string SerialNr,Array string aserialnr,Array val aserialnrqty,Integer acnt,var val qleft)
begin
  Boolean res;
  Integer i;
  
  res = false;
  for (i=0;i<acnt;i=i+1) begin
    if (aserialnr[i]==SerialNr) then begin
      if (aserialnrqty[i]!=0) then begin
        qleft = aserialnrqty[i];
        res = true;
      end;
    end;
  end;
  SerialNrCheck = res;
  return;
end;

procedure AddToSerialNr(Array string aserialnr,Array val aserialnrqty,var Integer acnt,val Qty,string SerialNr)
begin
  Integer i;
  
  for (i=0;i<acnt;i=i+1) begin
    if (SerialNr==aserialnr[i]) then begin
      aserialnrqty[i] = aserialnrqty[i] + Qty;
      goto LAddToSerialNr;
    end;
  end;
  aserialnr[acnt] = SerialNr;
  aserialnrqty[acnt] = Qty;
  acnt = acnt + 1;
LAddToSerialNr:;  
  return;
end;

global
function val FindCostPriceSerialNumberBackDated(Integer FIFOPerLocation,record INVc INr,string location,string serialnr,val qty,val adjustqty,Date td,Boolean costfrrecf,Integer print,Boolean ondayratef,Integer OverwriteCostModel,Integer consigstock)
//val adjustqty ? 
begin
  record ItemHistVc IHr;
  record ItemHistVc nextIHr;
  record ItemHistVc outIHr;
  record ItemHistVc printIHr;
  string 200 thekey;
  Integer keys;
  Boolean foundf,firstf,testf;
  val qcnt,res,instock,inprice,consiginstock,qusebefdat,qleft;
  val remcostprice,totcostprice,t,usedqty,outqty,nextqty;
  record CostAccBlock CAb;
  Integer acnt;
  Array string 255 aserialnr;
  Array val aserialnrqty;
  LongInt looppos;
  res = 0;
  

  if (blankdate(td)) or (td==CurrentDate) then begin//jjtmp
//  if (blankdate(td)) then begin//jjtmp
    inprice = FindCostPriceFIFO(FIFOPerLocation,INr,location,serialnr,qty,adjustqty,costfrrecf,print!=0,ondayratef,OverwriteCostModel,consigstock);
    goto LFindCostPriceSerialNumberBackDated;
  end;

  BlockLoad(CAb);
  IHr.ArtCode = INr.Code;
  IHr.Location = location;
  IHr.FIFODate = td;
  if ((FIFOPerLocation==0) or (blank(IHr.Location))) then begin
    thekey = "ArtCodeFD";
    keys = 2;
    instock = GetStockQty(INr.Code,";;;",td,false);
  end else begin
    thekey = "ArtCodeLocFD";
    keys = 3;
    instock = GetStockQty(INr.Code,location,td,false);
  end;

  switch (consigstock) begin
    case 0:  ;
    case 1: 
      if (blank(location)) then begin
        consiginstock = ConsigmentStockQty(INr.Code,";;;",td);
      end else begin
        consiginstock = ConsigmentStockQty(INr.Code,location,td);
      end;
      instock = instock - consiginstock;
    case 2: 
      if (blank(location)) then begin
        consiginstock = ConsigmentStockQty(INr.Code,";;;",td);
      end else begin
        consiginstock = ConsigmentStockQty(INr.Code,location,td);
      end;
      instock = consiginstock;
  end;
 
  ResetLoop(nextIHr);
  nextIHr.ArtCode = INr.Code;
  nextIHr.Location = location;
  SetLoopPosition(nextIHr,looppos-1);  

  inprice = 0;
  usedqty = 0;
  firstf = true;
  foundf = true;
  while (LoopKey(thekey,nextIHr,2,foundf)) begin // Start from the nextIHr record
    if (nextIHr.ArtCode!=INr.Code) then begin
      foundf = false;
    end;
    if (keys==3) then begin
      if (nextIHr.Location!=location) then begin
        foundf = false;
      end;
    end;
    if (nextIHr.FIFODate>td) then begin
      foundf = false;
    end;
    if (usedqty>=qty) then begin
      foundf = false;
    end;
    if (foundf) then begin
      testf = true;
      if (nextIHr.StockAffectf==0) then begin testf = false; end;
      if (nextIHr.Invalid!=0) then begin testf = false; end;
      if (nextIHr.Qty<0) then begin testf = false; end;
      if (nonblank(location)) then begin
        if (nextIHr.Location!=location) then begin
          testf = false;
        end;
      end;
      if (testf) then begin
        qusebefdat = QtyUsedBeforeDate(nextIHr,true,td);
        nextqty = nextIHr.Qty - qusebefdat;
        outqty = outqty - nextIHr.Qty - qusebefdat;
        if (outqty<0) then begin
          outqty = 0;
        end;
        if (nextqty<0) then begin
          testf = false;
        end;
      end;              
      if (testf) then begin     
        if ((usedqty+nextqty)>qty) then begin
          nextqty = qty - usedqty;
        end else begin
          nextqty = nextqty; 
        end;
        usedqty = usedqty + nextqty;
        switch (consigstock) begin
          case 1: 
            if (ConsigmentStock(nextIHr.ArtCode,nextIHr.SerialNr,td)) then begin
//            if (nextIHr.ConsigStock!=0) then begin 
              testf = false;
            end;
          case 2: 
            if (ConsigmentStock(nextIHr.ArtCode,nextIHr.SerialNr,td)==false) then begin
//            if (nextIHr.ConsigStock==0) then begin 
              testf = false;
            end;
        end;
        if (testf) then begin
        switch (OverwriteCostModel) begin 
          case 3:
            if (FIFOPerLocation!=0) then begin
              t = nextIHr.WATotCostPerLoc/nextIHr.Qty;
            end else begin
              t = nextIHr.WATotCost/nextIHr.Qty;
            end;
            t = FindWAPriceBackdated(FIFOPerLocation,INr,nextIHr.Location,1,td,false,0);
            t = Round(t,2);
            if (nextIHr.FileName=="RetVc") then begin
              if (CAb.RetFIFOQue!=0) then begin
                remcostprice = nextIHr.Qty*t;
              end else begin
                remcostprice = nextIHr.RemQty*t;
              end;
            end else begin
//              remcostprice = nextIHr.RemQty*t;
              if (nextqty>nextIHr.Qty) then begin
                remcostprice = nextIHr.Qty*t;
              end else begin
                remcostprice = nextqty*t;
              end;
            end;
            if (nextIHr.Qty>0) then begin
              totcostprice = nextIHr.Qty*t;
            end else begin
              totcostprice = nextIHr.Qty*t;
            end;
          otherwise
            remcostprice = nextIHr.TotCostPrice;
            totcostprice = nextIHr.TotCostPrice;
        end;
        if (costfrrecf) or (ondayratef) then begin
          GetCostsFromRecords(nextIHr,remcostprice,totcostprice,ondayratef,td);
        end;

        if (print==2) then begin
          if (remcostprice!=0) and (nextqty!=0) then begin
            RecordCopy(printIHr,nextIHr);
            switch (OverwriteCostModel) begin 
              case 3:
                if (nextIHr.FileName=="RetVc") then begin
                  if (CAb.RetFIFOQue!=0) then begin
                    printIHr.RemQty = nextIHr.Qty;
                  end;
                end;
            end;
            printIHr.RemQty = nextqty;
            PrintItemHistLine(printIHr,nextqty * Round((totcostprice / nextIHr.Qty),CostPriceDecimals));
          end;          
        end;
        
        inprice = inprice + nextqty * Round((totcostprice / nextIHr.Qty),CostPriceDecimals);        

        printIHr.RemQty = printIHr.RemQty + nextqty;
        end;
      end;
    end;
  end;
  if (print==1) then begin
    if (printIHr.TotCostPrice!=0) then begin
      PrintItemHistLine(printIHr,Round(printIHr.TotCostPrice,CostPriceDecimals));
    end;
  end;
LFindCostPriceSerialNumberBackDated:;  
  res = Round(inprice,CostPriceDecimals);
  FindCostPriceSerialNumberBackDated = res;
  return;
end;

global
updating procedure DeleteItemHistVc(string filename,LongInt sernr,Integer rownr)
begin
  record ItemHistVc IHr;
  Boolean found;

  found = true;
  IHr.FileName = filename;
  IHr.TransNr = sernr;
  IHr.Row = rownr;
  while (LoopKey("FNTransNr",IHr,3,found)) begin
    if (IHr.FileName!=filename) then begin found = false; end;
    if (IHr.TransNr!=sernr) then begin found = false; end;
    if (IHr.Row!=rownr) then begin found = false; end;
    if (found) then begin
      RecordDelete(IHr);
      StepBack(IHr);
    end;
  end;  
  return;
end;

global
updating procedure InvalidateItemHistVc(string filename,LongInt sernr,Integer rownr)
begin
  record ItemHistVc IHr;
  record ItemHistVc oldIHr;
  Boolean found;

  found = true;
  IHr.FileName = filename;
  IHr.TransNr = sernr;
  IHr.Row = rownr;
  while (LoopKey("FNTransNr",IHr,3,found)) begin
    if (IHr.FileName!=filename) then begin found = false; end;
    if (IHr.TransNr!=sernr) then begin found = false; end;
    if (IHr.Row!=rownr) then begin found = false; end;
    if (found) then begin
      RecordCopy(oldIHr,IHr);
      IHr.Invalid = 1;
      if (RecordUpdate(oldIHr,IHr,false)==0) then begin
      end;
    end;
  end;  
  return;
end;

global
function val FindCostPriceBP(Integer WAPerLocation,record INVc INr,string location)
begin
  val res;
  record ITVc ITr;
  record CostAccBlock CAb;

  BlockLoad(CAb);
  ITr.Code = INr.Group;
  if (ReadFirstMain(ITr,1,true)) then begin
  end;
  res = FindCostPriceBasePrice(CAb,INr,ITr);
  FindCostPriceBP = res;
  return;
end;

