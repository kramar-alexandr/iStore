// PurOrdJRn.hal    
/*Checked for 3.5 990314 by Jarek*/

#ifdef HAL

external procedure BeginCurncyTotals(var Integer);
external procedure AddCurncyTotals(var array string,var array val,var Integer,string,val);
external procedure PrintCurncyTotals(array string,array val,array val,array val,Integer,Integer,Integer,Integer,Integer,Boolean,Integer);
external procedure HTDetailLevel(Integer, var string);
external procedure HTSupplier(string, var string);
external procedure HTNr(Integer,Integer,LongInt,LongInt,var string);
external procedure HTCustClass(string,var string);
external procedure HTCustClassType(string,var string);
external function Boolean SetInSet2(string,string);
external procedure ClassTypef(string,string,var Boolean);



procedure RepPO( record POVc POp)
begin
  Integer i,rwcnt;
  row POVc POrw;
  record PLVc PLr;

  StartFormat(15);
  OutLongInt(0,"DblPOVc",POp.SerNr,false);
  OutDate(130,0,POp.TransDate,false);
  OutString(220,0,POp.PayDeal,false);
  EndFormat;
  StartFormat(15);
  OutString(50,0,POp.VECode,false);
  OutString(200,0,POp.Addr0,false);
  EndFormat;
  StartFormat(15);
  OutString(200,0,POp.Addr1,false);
  StartFormat(15);
  EndFormat;
  StartFormat(15);
  OutString(50,0,POp.OurContact,false);     
  OutString(200,0,POp.Addr2,false);
  EndFormat;
  if nonblank(POp.Addr3) then begin
    StartFormat(15);
    OutString(200,0,POp.Addr3,false);
    EndFormat;
  end;
  if nonblank(POp.VEContact) then begin
    StartFormat(15);
    OutString(200,0,POp.VEContact,false);     
    EndFormat;
  end;
  if nonblank(POp.CurncyCode) then begin
    StartFormat(15);
    OutString(460,0,POp.CurncyCode,false);
    EndFormat;
  end;
  rwcnt = MatRowCnt(POp);
  for (i = 0 ; i<rwcnt ;i=i+1) begin
    MatRowGet(POp,i,POrw);
    if (POrw.stp==1) then  begin
      StartFormat(15);
      OutString(20,0,POrw.ArtCode,false);
      OutVal(124,0,POrw.Quant,M4UVal,true);
      OutString(130,0,POrw.Spec,false);
      OutVal(340,0,POrw.Price,M423Val,true);
      OutVal(370,0,POrw.vRebate,M41Val,true);
      OutVal(460,0,POrw.Sum,M4Val,true);
      OutString(480,0,POrw.VATCode,true);
      
      ////
     PLr.ArtCode = POrw.ArtCode;
	 PLr.PLCode = "RRP_U";
	 if(readfirstmain(PLr,2,true))then begin
		  OutString(490,0,PLr.ExVatPrice,false);
	 end;
      
      EndFormat;

    end;
  end;
  Black_Divider(380,460);
  StartFormat(15);
  if (POp.ExportFlag!=0) then begin
    OutString(20,0,USetStr(4948),false);
  end;
  OutVal(260,0,POp.Sum1,M4Val,true);
  OutVal(360,0,POp.Sum3,M4Val,true);
  OutVal(460,0,POp.Sum4,M4Val,true);
  EndFormat;
return;
end;


global
procedure PurOrdJRn(record RcVc RepSpec)
begin
  record POVc POr;
  record CUVc VEr;
  row POVc POrw;
  record PLVc PLr;
  Boolean TrHs;
  val iv;
  val TotSum;
  Boolean testf;
  LongInt afr,ato;
  string 255 tstr;
  Integer rw;
  array val av1;
  array val av2;
  array val av3;
  array string 20 acur;
  Integer curcnt,rwcnt,i;

  rw = 1;
  afr = FirstInRange(RepSpec.f1,30);
  ato = LastInRange(RepSpec.f1,30);

  TotSum = 0;
  BeginCurncyTotals(curcnt);
  StartReportJob(USetStr(4941));
  if nonblank(RepSpec.f1) then begin
    HTNr(1823,1822,afr,ato,tstr);
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  HTDetailLevel(RepSpec.ArtMode,tstr);
  Header(rw,tstr,1);
  rw = rw + 1;
  HTSupplier(RepSpec.f2,tstr);
  if nonblank(tstr) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  if (nonblank(RepSpec.f3)) then begin
      tstr = USetStr(2768);
      tstr = tstr & ": " & RepSpec.f3;
      Header(rw,tstr,1);
      rw = rw + 1;
  end; 
  HTCustClass(RepSpec.f4,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end; 
  HTCustClassType(RepSpec.f5,tstr);
  if (nonblank(tstr)) then begin
    Header(rw,tstr,1);
    rw = rw + 1;
  end; 
  EndHeader;
  if ((RepSpec.ArtMode==2) or (RepSpec.ArtMode==0)) then  begin
    StartFormat(15);
    OutString(0,0,USetStr(4942),false);
    OutString(60,0,USetStr(4943),false);
    OutString(140,0,USetStr(4944),false);
    OutString(1,0,USetStr(4947),true);
    OutString(1,0,"",true);
    OutString(1,0,"Prices:",true);
    EndFormat;
  end;
  if (RepSpec.ArtMode==1)  then  begin
    StartFormat(15);
    OutString(0,0,USetStr(4942),false); //№
    OutString(130,0,USetStr(4943),false); //Дата
    EndFormat;
  end;
  POr.SerNr = afr;
  if (RepSpec.ArtMode==0) then begin
    Gray_Divider(0,1);
  end;
  TrHs = true;
  while LoopMain(POr,1,TrHs) begin
    if (ato<>-1) then begin
      if (POr.SerNr>ato) then begin
        TrHs = false;
      end;
    end;
    if (TrHs) then begin
      testf = true;
      if (nonblank(RepSpec.f2) and (RepSpec.f2<>POr.VECode)) then begin
         testf = false;
      end;
      if (nonblank(RepSpec.FirstAcc) and (RepSpec.FirstAcc<>POr.POClass)) then begin
         testf = false;
      end;
      if ((nonblank(RepSpec.f3)) and (RepSpec.f3<>POr.Location)) then begin 
        testf = false; 
      end;
      if (DateInRange(POr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin testf = false; end;
      if (testf) then begin
        VEr.Code = POr.VECode;
        if ReadFirstMain(VEr,1,true) then begin
          if (nonblank(RepSpec.f4)) then begin
            if (SetInSet2(RepSpec.f4,VEr.Classification)==false) then begin
              testf = false;
            end;
          end;
          if (nonblank(RepSpec.f5)) then begin
            if (testf) then begin
              testf = false;
              ClassTypef(RepSpec.f5,VEr.Classification,testf);
            end;
          end;
        end;  
      end; 
      if (testf) then begin
        if ((RepSpec.ArtMode==0) or (RepSpec.ArtMode==1)) then begin
          iv = POr.Sum4;
        end;
        if (RepSpec.ArtMode==2) then begin
          iv = POr.Sum1;
        end;
        TotSum = TotSum + iv;
        AddCurncyTotals(acur,av1,curcnt,POr.CurncyCode,iv);
        if  (RepSpec.ArtMode==1) then begin
          Gray_Divider(0,1);
          RepPO(POr);
        end;
        if ((RepSpec.ArtMode==0) or (RepSpec.ArtMode==2)) then begin
          StartFormat(15);
          OutLongInt(0,"DblPOVc",POr.SerNr,false);
          OutDate(60,0,POr.TransDate,false);
          OutString(140,0,POr.Addr0,false);
          OutVal(440,0,iv,M4Val,true);
          OutString(445,0,POr.CurncyCode,false);
          //Журнал замовлень постачальника
          rwcnt = MatRowCnt(POr);
          for (i = 0 ; i<rwcnt ;i=i+1) begin
          	MatRowGet(POr,i,POrw);
          	if (POrw.stp==1) then begin
          	
			  PLr.ArtCode = POrw.ArtCode;
			  PLr.PLCode = "RRP_U";
				  if(readfirstmain(PLr,2,true))then begin
					  OutString(490,0,PLr.ExVatPrice,false);
				  end;
          	end;
          end;
          
          EndFormat;
         end;
      end;
    end;
  end;
  Gray_Divider(0,1);
  if (RepSpec.ArtMode==1) then begin
    PrintCurncyTotals(acur,av1,av2,av3,curcnt,460,-1,-1,140,true,RepSpec.PocketPC);
  end else begin
    PrintCurncyTotals(acur,av1,av2,av3,curcnt,440,-1,-1,140,true,RepSpec.PocketPC);
  end; 
  EndJob;
  return;
end;
#endif
